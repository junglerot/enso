from Base import all

import Table.Io.Csv
import Table.Data.Table
import Table.Data.Column

from Table.Io.Csv export all hiding Parser
export Table.Data.Column
from Table.Data.Table export new

## Converts a JSON array into a dataframe, by looking up the requested keys
   from each item.

   It assumes the items are JSON objects containing the requested keys.
   In case an item is not an object, or the request key does not exist, the
   relevant values of the table will be set to `Nothing`.

   Arguments:
     - fields: a vector of texts representing the names of fields to look up.
Json.Array.to_table : Vector -> Table
Json.Array.to_table fields = case this of
    Json.Array items ->
        rows = items.map <| case _ of
            Json.Object fs ->
                fields.map n-> case fs.get n of
                    Nothing -> Nothing
                    js -> js.unwrap
            _ -> Vector.fill fields.length Nothing
        cols = fields.map_with_index i-> n->
            [n, rows.map (_.at i)]
        Table.new cols

