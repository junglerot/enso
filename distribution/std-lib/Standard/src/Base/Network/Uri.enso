from Standard.Base import all

import Standard.Base.Network.Uri.Internal

polyglot java import java.net.URI as Java_URI
polyglot java import java.util.Optional

## Syntax error when parsing a Uri.
type Syntax_Error message

Syntax_Error.to_display_text =
    "Uri syntax error: " + this.message

## Convert Text to Uri.
Text.to_uri = here.parse this

type Uri
    ## Represents a Uniform Resource Identifier (URI) reference.
    type Uri internal_uri

    ## Convert this to Uri.
    to_uri : Uri
    to_uri = this

    ## Get scheme part of this Uri.

       > Example
         Return the "http" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . scheme
    scheme : Text ! Nothing
    scheme = Internal.handle_nothing this.internal_uri.getScheme

    ## Get user info part of this Uri.

       > Example
         Return the "user:pass" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . user_info
    user_info : Text ! Nothing
    user_info = Internal.handle_nothing this.internal_uri.getUserInfo

    ## Get host part of this Uri.

       > Example
         Return the "example.com" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . host
    host : Text ! Nothing
    host = Internal.handle_nothing this.internal_uri.getHost

    ## Get authority (user info and host) part of this Uri.

       > Example
         Return the "user:pass@example.com" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . authority
    authority : Text ! Nothing
    authority = Internal.handle_nothing this.internal_uri.getAuthority

    ## Get port part of this Uri.

       > Example
         Return the "80" part of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . port

       > Example
         Return the empty string if the port is not specified.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . port
    port : Text ! Nothing
    port =
        port_number = this.internal_uri.getPort
        Internal.handle_nothing <|
            if port_number == -1 then Nothing else port_number.to_text

    ## Get path part of this Uri.

       > Example
         Return the "/foo/bar" part of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . path
    path : Text ! Nothing
    path = Internal.handle_nothing this.internal_uri.getPath

    ## Get query part of this Uri.

       > Example
         Return the "key=val" part of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . query
    query : Text ! Nothing
    query = Internal.handle_nothing this.internal_uri.getQuery

    ## Get fragment part of this Uri.

       > Example
         Return the empty fragment of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . fragment
    fragment : Text ! Nothing
    fragment = Internal.handle_nothing this.internal_uri.getFragment

    ## Get unescaped user info part of this Uri.
    raw_user_info : Text ! Nothing
    raw_user_info = Internal.handle_nothing this.internal_uri.getRawUserInfo

    ## Get unescaped authority part of this Uri.
    raw_authority : Text ! Nothing
    raw_authority = Internal.handle_nothing this.internal_uri.getRawAuthority

    ## Get unescaped path part of this Uri.
    raw_path : Text ! Nothing
    raw_path = Internal.handle_nothing this.internal_uri.getRawPath

    ## Get unescaped query part of this Uri.
    raw_query : Text ! Nothing
    raw_query = Internal.handle_nothing this.internal_uri.getRawQuery

    ## Get unescaped fragment part of this Uri.
    raw_fragment : Text ! Nothing
    raw_fragment = Internal.handle_nothing this.internal_uri.getRawFragment

    ## Convert this Uri to text.
    to_text : Text
    to_text = this.internal_uri.toString

    ## An Uri to JSON conversion.
    to_json : Json.String
    to_json : Json.String this.to_text

    ## Check Uri equality.
    == : Uri -> Boolean
    == that = this.internal_uri.equals that.internal_uri

## Parse Uri from text.
   Return Syntax_Error when the text cannot be parsed as Uri.

   > Example
     Parse Uri text.
         Uri.parse "http://example.com"
parse : Text -> Uri ! Syntax_Error
parse text =
    Panic.recover (Uri (Java_URI.create text)) . catch e-> case e of
        Polyglot_Error ex -> Error.throw (Syntax_Error ex.getMessage)
        other -> Panic.throw other
