from Standard.Base import all

import Standard.Base.Data.Time.Time

polyglot java import java.time.Duration as Java_Duration
polyglot java import java.time.Period as Java_Period

type Duration

    ## An amount of time in terms of years, months, days, hours, minutes,
       seconds and nanoseconds.

       Arguments:
       - internal_period: The internal representation of the time as a period.
       - internal_duration: The internal representation of the time as a
         duration.
    type Duration internal_period internal_duration

    ## Add the specified amount of time to this duration.

       Arguments:
       - that: The duration to add to `this`.

       > Example
         Add 6 seconds to a duration of 3 minutes
             3.minutes + 6.seconds

       > Example
         Add 12 hours to a duration of a month.
             1.month + 12.hours
    + : Duration -> Duration
    + that = Duration (this.internal_period . plus that.internal_period . normalized) (this.internal_duration . plus that.internal_duration)

    ## Subtract the specified amount of time from this duration.

       Arguments:
       - that: The duration to subtract from `this`.

       > Example
         Subtract 11 months from a duration of 3 years
             3.years - 11.months

       > Example
         Substract 30 minutes from a duration of 7 months.
             7.months - 30.minutes
    - : Duration -> Duration
    - that = Duration (this.internal_period . minus that.internal_period . normalized) (this.internal_duration . minus that.internal_duration)

    ## Get the portion of the duration expressed in nanoseconds.

       > Example
         Get the portion of the duration expressed in nanoseconds.
             1.nanosecond.nanoseconds
    nanoseconds : Integer
    nanoseconds = this.internal_duration . toNanosPart

    ## Get the portion of the duration expressed in milliseconds.

       > Example
         Get the portion of the duration expressed in milliseconds.
             1.millisecond.milliseconds
    milliseconds : Integer
    milliseconds = this.internal_duration . toMillisPart

    ## Get the portion of the duration expressed in seconds.

       > Example
         Get the portion of the duration expressed in seconds.
             1.second.seconds
    seconds : Integer
    seconds = this.internal_duration . toSecondsPart

    ## Get the portion of the duration expressed in minutes.

       > Example
         Get the portion of the duration expressed in minutes.
             1.minute.minutes
    minutes : Integer
    minutes = this.internal_duration . toMinutesPart

    ## Get the portion of the duration expressed in hours.

       > Example
         Get the portion of the duration expressed in hours.
             1.hour.hours
    hours : Integer
    hours = this.internal_duration . toHours

    ## Get the portion of the duration expressed in days.

       > Example
         Get the portion of the duration expressed in days.
             1.day.days
    days : Integer
    days = this.internal_period . getDays

    ## Get the portion of the duration expressed in months.

       > Example
         Get the portion of the duration expressed in months.
             1.month.months
    months : Integer
    months = this.internal_period . getMonths

    ## Get the portion of the duration expressed in years.

       > Example
         Get the portion of the duration expressed in years.
             1.year.years
    years : Integer
    years = this.internal_period . getYears

    ## Convert this duration to a Vector of years, months, days, hours, minutes,
       seconds and nanosecnods.

       > Example
         Convert duration of a year and a hour to a vector returning `[1, 0, 0, 1, 0, 0, 0]`.
             1.year . plus 1.hour . to_vector

       > Example
         Convert duration of 800 nanoseconds to a vector returning `[0, 0, 0, 0, 0, 0, 800]`
             800.nanoseconds . to_vector
    to_vector : Vector.Vector
    to_vector = [this.years, this.months, this.days, this.hours, this.minutes, this.seconds, this.nanoseconds]

    ## A Duration to Json conversion.

       > Example
         Convert a duration of 10 seconds to Json.
             10.seconds.to_json
    to_json : Json.Object
    to_json =
        b = Vector.new_builder
        b.append ["type", "Duration"]
        if this.years==0 . not then b.append ["years", this.years]
        if this.months==0 . not then b.append ["months", this.months]
        if this.days==0 . not then b.append ["days", this.days]
        if this.hours==0 . not then b.append ["hours", this.hours]
        if this.minutes==0 . not then b.append ["minutes", this.minutes]
        if this.seconds==0 . not then b.append ["seconds", this.seconds]
        if this.nanoseconds==0 . not then b.append ["nanoseconds", this.nanoseconds]
        Json.from_pairs b.to_vector

    ## Check if this duration is date-based.

       > Example
         Check if the duration of 10 seconds is date-based.
             10.seconds.is_date
    is_date : Boolean
    is_date = (this.years==0 . not) || (this.months==0 . not) || (this.days==0 . not)

    ## Check if this duration is time-based.

       > Example
         Check if the duration of 10 seconds is time-based.
             10.seconds.is_time
    is_time : Boolean
    is_time = (this.hours==0 . not) || (this.minutes==0 . not) || (this.seconds==0 . not) || (this.nanoseconds==0 . not)

    ## Check if this duration represents an empty time-span.

       > Example
         Check if the duration of 10 seconds is empty.
             10.seconds.is_empty
    is_empty : Boolean
    is_empty = this.is_date.not && this.is_time.not

    ## Check two durations for equality.

       Arguments:
       - that: The duration to compare against `this`.

       > Examples
         Check if 60 seconds and 1 minute are equal.
             60.seconds == 1.minute
    == : Duration -> Boolean
    == that = this.to_vector == that.to_vector

## Duration in nanoseconds.
Integer.nanosecond : Duration
Integer.nanosecond = Duration (Java_Period.ofDays 0) (Java_Duration.ofNanos this)

## Duration in nanoseconds.
Integer.nanoseconds : Duration
Integer.nanoseconds = this.nanosecond

## Duration in milliseconds.
Integer.millisecond : Duration
Integer.millisecond = Duration (Java_Period.ofDays 0) (Java_Duration.ofMillis this)

## Duration in milliseconds.
Integer.milliseconds : Duration
Integer.milliseconds = this.millisecond

## Duration in seconds.
Integer.second : Duration
Integer.second = Duration (Java_Period.ofDays 0) (Java_Duration.ofSeconds this)

## Duration in seconds.
Integer.seconds : Duration
Integer.seconds = this.second

## Duration in minutes.
Integer.minute : Duration
Integer.minute = Duration (Java_Period.ofDays 0) (Java_Duration.ofMinutes this)

## Duration in minutes.
Integer.minutes : Duration
Integer.minutes = this.minute

## Duration in hours.
Integer.hour : Duration
Integer.hour = Duration (Java_Period.ofDays 0) (Java_Duration.ofHours this)

## Duration in hours.
Integer.hours : Duration
Integer.hours = this.hour

## Duration in days.
Integer.day : Duration
Integer.day = Duration (Java_Period.ofDays this . normalized) (Java_Duration.ofSeconds 0)

## Duration in days.
Integer.days : Duration
Integer.days = this.day

## Duration in months.
Integer.month : Duration
Integer.month = Duration (Java_Period.ofMonths this . normalized) (Java_Duration.ofSeconds 0)

## Duration in months.
Integer.months : Duration
Integer.months = this.month

## Duration in years.
Integer.year : Duration
Integer.year = Duration (Java_Period.ofYears this . normalized) (Java_Duration.ofSeconds 0)

## Duration in years.
Integer.years : Duration
Integer.years = this.year

## Create an interval representing the duration between two points in time.

   Arguments:
   - start_inclusive: The start time of the duration.
   - end_inclusife: The end time of the duration.

   > Example
     An hour interval between two points in time.
         Duration.between Time.now (Time.new 2010 10 20)
between : Time -> Time -> Duration
between start_inclusive end_exclusive =
   Duration (Java_Period.ofDays 0 . normalized) (Java_Duration.between start_inclusive.internal_zoned_date_time end_exclusive.internal_zoned_date_time)

