from Standard.Base import all

import Standard.Base.Data.Map.Internal

## UNSTABLE

   An error for getting a missing value from a map.

   Arguments:
   - key: The key that was asked for.
type No_Value_For_Key key

## Returns an empty map.

   > Example
     Create an empty map.
         Map.empty
empty : Map
empty = Tip

## Returns a single-element map with the given key and value present.

   Arguments:
   - key: The key to update in the map.
   - value: The value to store against 'key' in the map.

   > Example
     Create a single element map storing the key 1 and the value 2.
         Map.singleton 1 2
singleton : Any -> Any -> Map
singleton key value = Bin 1 key value Tip Tip

## Builds a map from a vector of key-value pairs.

   Arguments:
   - vec: A vector of key-value pairs.

   > Example
     Building a map containing two key-value pairs.
         Map.from_vector [[1, 2], [3, 4]]
from_vector : Vector.Vector -> Map
from_vector vec = vec.fold Map.empty (m -> el -> m.insert (el.at 0) (el.at 1))

## A key-value store. This type assumes all keys are pairwise comparable,
   using the `<`, `>` and `==` operators.
type Map

    ## PRIVATE
       A key-value store. This type assumes all keys are pairwise comparable,
       using the `<`, `>` and `==` operators.
    type Tip

    ## PRIVATE
       A key-value store. This type assumes all keys are pairwise comparable,
       using the `<`, `>` and `==` operators.

       Arguments:
       - s: The size of the tree at this node.
       - key: The key stored at this node.
       - value: The value stored at this node.
       - left: The left subtree.
       - right: The right subtree.
    type Bin s key value left right

    ## Checks if the map is empty.

       > Example
         Check if a singleton map is empty.
             Map.singleton 1 2 . is_empty
    is_empty : Boolean
    is_empty = case this of
        Bin _ _ _ _ _ -> False
        Tip -> True

    ## Checks if the map is not empty.

       > Example
         Check if a singleton map is not empty.
             Map.singleton 1 2 . not_empty
    not_empty : Boolean
    not_empty = this.is_empty.not

    ## Returns the number of entries in this map.

       > Example
         Get the size of a singleton map.
             Map.singleton 1 2 . size
    size : Integer
    size = case this of
        Bin s _ _ _ _ -> s
        Tip -> 0

    ## Converts the map into a vector of `[key, value]` pairs.

       The returned vector is sorted in the increasing order of keys.

       > Example
         Convert a singleton map to a vector.
             Map.singleton 1 2 . to_vector
    to_vector : Vector.Vector
    to_vector =
        builder = Vector.new_builder
        to_vector_with_builder m = case m of
            Bin _ k v l r ->
                to_vector_with_builder l
                builder.append [k, v]
                to_vector_with_builder r
                Nothing
            Tip -> Nothing
        to_vector_with_builder this
        result = builder.to_vector
        result

    ## Returns a text representation of this map.

       > Example
         Convert an empty map to text.
             Map.empty.to_text
    to_text : Text
    to_text = this.to_vector.to_text

    ## Checks if this map is equal to another map.

       Arguments:
       - that: The map to compare `this` to.

       Maps are equal when they contained the same keys and the values
       associated with each key are pairwise equal.

       > Example
         Checking two singleton maps for equality.
             (Map.singleton 1 2) == (Map.singleton 2 3)
    == : Map -> Boolean
    == that = this.to_vector == that.to_vector

    ## Inserts a key-value mapping into this map, overriding any existing
       instance of `key` with the new `value`.

       Arguments:
       - key: The key to insert the value for.
       - value: The value to associate with `key`.

       > Example
         Insert the value 3 into a map for the key 1.
             Map.empty.insert 1 3
    insert : Any -> Any -> Map
    insert key value = Internal.insert this key value

    ## Gets the value associated with `key` in this map, or throws a `Nothing`,
       if `key` is not present.

       Arguments:
       - key: The key to look up in the map.

       > Example
         Get the value for the key 2 in a map.
             Map.empty.get 2
    get : Any -> Any ! Nothing
    get key =
        go map = case map of
            Tip -> Error.throw Nothing
            Bin _ k v l r ->
                if k == key then v else
                    if k > key then @Tail_Call go l else @Tail_Call go r
        result = go this
        result

    ## Gets the value associated with `key` in this map, or returns `other` if
       it isn't present.

       Arguments:
       - key: The key to look up in the map.
       - other: The value to use if the key isn't present.

       > Example
         Get the value for the key 2 in a map or instead return 10 if it isn't
         present.
             Map.empty.get_or_else 2 10
    get_or_else : Any -> Any -> Any
    get_or_else key ~other =
        this.get key . catch (_ -> other)

    ## Transforms the map's keys and values to create a new map.

       Arguments:
       - function: The function used to transform the map, taking a key and a
         value and returning a pair of `[key, value]`.

       > Example
         Turn all keys into `Text` and double the values for a map `m`.
             m.transform (k -> v -> [k.to_text, v*2])
    transform : (Any -> Any -> [Any, Any]) -> Map
    transform function =
        func_pairs = p -> function (p.at 0) (p.at 1)
        vec_transformed = this.to_vector.map func_pairs
        here.from_vector vec_transformed

    ## Maps a function over each value in this map.

       Arguments:
       - function: The function to apply to each value in the map, taking a
         value and returning a value.

       > Example
         Turn all values into `Text` for a map `m`.
             m.map (v -> v.to_text)
    map : (Any -> Any) -> Map
    map function =
        kv_func = _ -> function
        this.map_with_key kv_func

    ## Maps a function over each key-value pair in the map, transforming the
       value.

       Arguments:
       - function: The function to apply to each key and value in the map,
         taking a key and a value and returning a value.

       > Example
         Adding the key to the value in a map `m`.
             m.map_with_key (k -> v -> k + v)
    map_with_key : (Any -> Any -> Any) -> Map
    map_with_key function =
        go map = case map of
            Bin s k v l r ->
                Bin s k (function k v) (go l) (go r)
            Tip -> Tip
        go this

    ## Maps a function over each key in this map.

       Arguments:
       - function: The function to apply to each key in the map, taking a key
         and returning a key.

       > Example
         Doubling all keys in the map `m`.
             m.map_keys (k -> k*2)
    map_keys : (Any -> Any) -> Map
    map_keys function =
        trans_function = k -> v -> [function k, v]
        this.transform trans_function

    ## Applies a function to each value in the map.

       Arguments:
       - function: The function to apply to each value in the map, taking a
         value and returning anything.

       This method does not return the results, so is only useful for performing
       computations with side-effects.

       > Example
         Printing each value in the map `m`.
             m.each IO.println
    each : (Any -> Any) -> Nothing
    each function =
        kv_func = _ -> function
        this.each_with_key kv_func

    ## Applies a function to each key-value pair in the map.

       Arguments:
       - function: The function to apply to each key-value pair in the map,
         taking a key and a value and returning anything.

       This method does not return the results, so is only useful for performing
       computations with side-effects.

       > Example
         Printing each key and value in the map `m`.
             m.each (k -> v -> IO.println (k.to_text + v.to_text))
    each_with_key : (Any -> Any -> Any) -> Nothing
    each_with_key function =
        go map = case map of
            Bin _ k v l r ->
                go l
                function k v
                go r
                Nothing
            Tip -> Nothing
        go this

    ## Combines the values in the map.

       Arguments:
       - init: The initial value for the fold.
       - function: A binary function to apply to pairs of values in the map.

       > Example
         Summing all of the values in the map `m`.
             m.fold 0 (+)
    fold : Any -> (Any -> Any -> Any) -> Any
    fold init function =
        go map init = case map of
            Bin _ _ v l r ->
                y = go l init
                z = function y v
                go r z
            Tip -> init
        go this init

    ## Combines the key-value pairs in the map.

       Arguments:
       - init: The initial value for the fold.
       - function: A function taking the left value, the current key, and the
         current value, and combining them to yield a single value.

       > Example
         Sum the keys and values in the map `m`.
             m.fold_with_key 0 (l -> k -> v -> l + k + v)
    fold_with_key : Any -> (Any -> Any -> Any -> Any) -> Any
    fold_with_key init function =
        go map init = case map of
            Bin _ k v l r ->
                y = go l init
                z = function y k v
                go r z
            Tip -> init
        go this init

    ## Get a vector containing the keys in the map.

       > Example
         Get the keys from the map `m`.
             m.keys
    keys : Vector
    keys =
        builder = Vector.new_builder
        to_vector_with_builder m = case m of
            Bin _ k _ l r ->
                to_vector_with_builder l
                builder.append k
                to_vector_with_builder r
                Nothing
            Tip -> Nothing
        to_vector_with_builder this
        builder.to_vector

    ## Get a vector containing the values in the map.

       > Example
         Get the values from the map `m`.
             m.values
    values : Vector
    values =
        builder = Vector.new_builder
        to_vector_with_builder m = case m of
            Bin _ _ v l r ->
                to_vector_with_builder l
                builder.append v
                to_vector_with_builder r
                Nothing
            Tip -> Nothing
        to_vector_with_builder this
        builder.to_vector
