import project.Data.Text.Text
import project.Error.Error
import project.Panic.Panic

polyglot java import java.io.IOException
polyglot java import java.nio.file.AccessDeniedException
polyglot java import java.nio.file.NoSuchFileException
polyglot java import java.nio.file.FileAlreadyExistsException

## Errors when reading or writing to a file.
type File_Error
    ## An error that indicates that the requested file does not exist.

       Arguments:
       - file: The file that doesn't exist.
    Not_Found file

    ## Indicates that a destination file already exists.
    Already_Exists file

    ## A generic IO error.

       Arguments:
       - file: The file that couldn't be read.
       - message: The message for the error.
    IO_Error file message

    ## Indicates that the given file's type is not supported.
    Unsupported_Type file

    ## UNSTABLE

       Convert the File error to a human-readable format.
    to_display_text : Text
    to_display_text self = case self of
        File_Error.Not_Found  file -> "The file at " + file.path + " does not exist."
        File_Error.IO_Error file msg -> msg.to_text + " (" + file.path + ")."
        File_Error.Already_Exists file -> "The file at "+file.path+" already exists."
        File_Error.Unsupported_Type file -> "The "+file.path+" has a type that is not supported."

    ## PRIVATE

       Utility method for running an action with Java exceptions mapping.
    handle_java_exceptions file ~action =
        Panic.catch IOException action caught_panic->
            File_Error.wrap_io_exception file caught_panic.payload

    ## PRIVATE

       Converts a Java `IOException` into its Enso counterpart.
    wrap_io_exception file io_exception = case io_exception of
        _ : NoSuchFileException -> Error.throw (File_Error.Not_Found file)
        _ : FileAlreadyExistsException -> Error.throw (File_Error.Already_Exists file)
        _ : AccessDeniedException -> Error.throw (File_Error.IO_Error file "You do not have permission to access the file")
        _ -> Error.throw (File_Error.IO_Error file "An IO error has occurred: "+io_exception.to_text)
