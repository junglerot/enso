import project.Data.Numbers.Integer
import project.Error.Error
import project.Errors.Illegal_Argument.Illegal_Argument
from project.Data.Boolean import Boolean, False, True
from project.Data.Ordering import all

polyglot java import java.time.DayOfWeek

## Represents a day of the week.
type Day_Of_Week
    ## Day_Of_Week representing Sunday.
    Sunday

    ## Day_Of_Week representing Monday.
    Monday

    ## Day_Of_Week representing Tuesday.
    Tuesday

    ## Day_Of_Week representing Wednesday.
    Wednesday

    ## Day_Of_Week representing Thursday.
    Thursday

    ## Day_Of_Week representing Friday.
    Friday

    ## Day_Of_Week representing Saturday.
    Saturday

    ## GROUP Conversions
       Convert the Day_Of_Week to an Integer.

       Arguments:
       - `first_day`: The first day of the week.
       - `start_at_zero`: If True, first day of the week is 0 otherwise is 1.
    to_integer : Day_Of_Week -> Boolean -> Integer
    to_integer self first_day=Day_Of_Week.Sunday start_at_zero=False =
        day_number = case self of
            Day_Of_Week.Sunday -> 0
            Day_Of_Week.Monday -> 1
            Day_Of_Week.Tuesday -> 2
            Day_Of_Week.Wednesday -> 3
            Day_Of_Week.Thursday -> 4
            Day_Of_Week.Friday -> 5
            Day_Of_Week.Saturday -> 6

        shifted = case first_day of
            Day_Of_Week.Sunday -> day_number
            _ -> (day_number + 7 - (first_day.to_integer start_at_zero=True)) % 7

        shifted + if start_at_zero then 0 else 1

    ## PRIVATE
    to_java : DayOfWeek
    to_java self = case self of
        Day_Of_Week.Sunday -> DayOfWeek.SUNDAY
        Day_Of_Week.Monday -> DayOfWeek.MONDAY
        Day_Of_Week.Tuesday -> DayOfWeek.TUESDAY
        Day_Of_Week.Wednesday -> DayOfWeek.WEDNESDAY
        Day_Of_Week.Thursday -> DayOfWeek.THURSDAY
        Day_Of_Week.Friday -> DayOfWeek.FRIDAY
        Day_Of_Week.Saturday -> DayOfWeek.SATURDAY

## PRIVATE
type Day_Of_Week_Comparator
    ## PRIVATE
    compare : Day_Of_Week -> Day_Of_Week -> Ordering
    compare x y =
        x_int = x.to_integer
        y_int = y.to_integer
        Comparable.from x_int . compare x_int y_int

    ## PRIVATE
    hash : Day_Of_Week -> Integer
    hash x = x.to_integer

## PRIVATE
Comparable.from (_:Day_Of_Week) = Day_Of_Week_Comparator

## Convert from an integer to a Day_Of_Week

   Arguments:
   - `that`: The first day of the week.
   - `first_day`: The first day of the week.
   - `start_at_zero`: If True, first day of the week is 0 otherwise is 1.
Day_Of_Week.from (that : Integer) (first_day:Day_Of_Week=Day_Of_Week.Sunday) (start_at_zero:Boolean=False) =
    shifted = if start_at_zero then that else that - 1

    case (shifted < 0) || (shifted > 6) of
        True ->
            valid_range = if start_at_zero then "0-6" else "1-7"
            message = "Invalid day of week (must be " + valid_range + ")."
            Error.throw (Illegal_Argument.Error message)
        False ->
            day_number = if first_day == Day_Of_Week.Sunday then shifted else
                (shifted + (first_day.to_integer start_at_zero=True)) % 7

            [Day_Of_Week.Sunday, Day_Of_Week.Monday, Day_Of_Week.Tuesday, Day_Of_Week.Wednesday, Day_Of_Week.Thursday, Day_Of_Week.Friday, Day_Of_Week.Saturday].at day_number
