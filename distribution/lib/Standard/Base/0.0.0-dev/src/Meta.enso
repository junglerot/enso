from Standard.Base import all hiding Java
import Standard.Base

# TODO Dubious constructor export
from project.Meta.Atom import all
from project.Meta.Atom export all

## UNSTABLE
   ADVANCED

   An Atom meta-representation.

   Arguments:
   - value: The value of the atom in the meta representation.
type Atom
    Atom_Data value

# TODO Dubious constructor export
from project.Meta.Constructor import all
from project.Meta.Constructor export all

## UNSTABLE
   ADVANCED

   A constructor meta-representation.

   Arguments:
   - value: The value of the constructor in the meta representation.
type Constructor
    Constructor_Data value

# TODO Dubious constructor export
from project.Meta.Primitive import all
from project.Meta.Primitive export all

## UNSTABLE
   ADVANCED

   A primitive value meta-prepresentation.

   Arguments:
   - value: The value of the primitive object in the meta representation.
type Primitive
    Primitive_Data value

# TODO Dubious constructor export
from project.Meta.Unresolved_Symbol import all
from project.Meta.Unresolved_Symbol export all

## UNSTABLE
   ADVANCED

   An unresolved symbol meta-representation.

   Arguments:
   - value: The value of the unresolved symbol in the meta representation.
type Unresolved_Symbol
    Unresolved_Symbol_Data value

    ## UNSTABLE
       ADVANCED

       Returns a new unresolved symbol with its name changed to the provided
       argument.

       Arguments:
       - new_name: The new name for the unresolved symbol.
    rename : Text -> Any
    rename self new_name =
        create_unresolved_symbol new_name self.scope

    ## UNSTABLE
       ADVANCED

       Returns the name of an unresolved symbol.
    name : Text
    name self = get_unresolved_symbol_name self.value

    ## UNSTABLE
       ADVANCED

       Returns the definition scope of an unresolved symbol.
    scope : Any
    scope self = get_unresolved_symbol_scope self.value

# TODO Dubious constructor export
from project.Meta.Error import all
from project.Meta.Error export all

## UNSTABLE
   ADVANCED

   An error meta-representation, containing the payload of a dataflow error.

   Arguments:
   - value: The payload of the error.
type Error
    Error_Data value

# TODO Dubious constructor export
from project.Meta.Polyglot import all
from project.Meta.Polyglot export all

## UNSTABLE
   ADVANCED

   A polyglot value meta-representation.

   Arguments:
   - value: The polyglot value contained in the meta representation.
type Polyglot
    Polyglot_Data value


## Atom methods
## PRIVATE

   Gets the atom constructor instance for the provided atom.

   Arguments:
   - atom: The atom to obtain the constructor for.
get_atom_constructor : Atom -> Atom_Constructor
get_atom_constructor atom = @Builtin_Method "Meta.get_atom_constructor"

## PRIVATE

   Get the fields for the provided atom.

   Arguments:
   - atom: The atom to obtain the fields for.
get_atom_fields : Atom -> Array
get_atom_fields atom = @Builtin_Method "Meta.get_atom_fields"

## UNSTABLE
   ADVANCED

   Returns a vector of field values of the given atom.
Atom.fields : Vector.Vector
Atom.fields self = Vector.from_polyglot_array (get_atom_fields self.value)

## UNSTABLE
   ADVANCED

   Returns a constructor value of the given atom.
Atom.constructor : Atom_Constructor
Atom.constructor self = get_atom_constructor self.value ...

# Polyglot methods
## PRIVATE

   Get a textual representation of the language from which an object comes.

   Arguments:
   - value: The value to obtain the source language for.
get_polyglot_language : Any -> Text
get_polyglot_language value = @Builtin_Method "Meta.get_polyglot_language"

## UNSTABLE
   ADVANCED

   Returns the language with which a polyglot value is associated.
Polyglot.get_language : Language
Polyglot.get_language self =
    lang_str = get_polyglot_language self.value
    if lang_str == "java" then Java else Unknown

# UnresolvedSymbol methods
## PRIVATE

   Creates an unresolved symbol for the name name in the scope.

   Arguments:
   - name: The name of the unresolved symbol.
   - scope: The scope in which the symbol name is unresolved.
create_unresolved_symbol : Text -> Module_Scope -> Unresolved_Symbol
create_unresolved_symbol name scope = @Builtin_Method "Meta.create_unresolved_symbol"

## PRIVATE

   Obtains the name of the provided unresolved symbol.

   Arguments:
   - symbol: The unresolved symbol from which to get the name.
get_unresolved_symbol_name : Unresolved_Symbol -> Text
get_unresolved_symbol_name symbol = @Builtin_Method "Meta.get_unresolved_symbol_name"

## PRIVATE

   Obtains the scope in which the provided unresolved symbol was created.

   Arguments:
   - symbol: The unresolved symbol from which to get the scope.
get_unresolved_symbol_scope : Unresolved_Symbol -> Module_Scope
get_unresolved_symbol_scope symbol = @Builtin_Method "Meta.get_unresolved_symbol_scope"

# Constructor methods
## PRIVATE

   Get the fields of an atom constructor.

   Arguments:
   - atom_constructor: The constructor from which to get the fields.
get_constructor_fields : Atom_Constructor -> Array
get_constructor_fields atom_constructor = @Builtin_Method "Meta.get_constructor_fields"

## PRIVATE

   Get the name of an atom constructor.

   Arguments:
   - atom_constructor: The atom constructor from which to obtain the name.
get_constructor_name : Atom_Constructor -> Text
get_constructor_name atom_constructor = @Builtin_Method "Meta.get_constructor_name"

## PRIVATE

   Constructs a new atom using the provided constructor and fields.

   Arguments:
   - constructor: The constructor for the atom to create.
   - fields: The arguments to pass to constructor.
new_atom : Atom_Constructor -> Array -> Atom
new_atom constructor fields = @Builtin_Method "Meta.new_atom"

## UNSTABLE
   ADVANCED

   Returns a vector of field names defined by a constructor.
Constructor.fields : Vector.Vector
Constructor.fields self = Vector.from_polyglot_array (get_constructor_fields self.value)

## UNSTABLE
   ADVANCED

   Returns the name of a constructor.
Constructor.name : Text
Constructor.name self = get_constructor_name self.value

## UNSTABLE
   ADVANCED

   Creates a new atom of the given constructor.

   Arguments:
   - fields: A vector of arguments to pass to the constructor when creating the
     new atom.
Constructor.new : Vector.Vector -> Any
Constructor.new self fields = new_atom self.value fields.to_array


## UNSTABLE
   ADVANCED

   Returns a meta-representation of a given runtime entity.

   Arguments:
   - value: The runtime entity to get the meta representation of.
meta : Any -> Meta
meta value = if is_atom value then Atom_Data value else
    if is_atom_constructor value then Constructor_Data value else
        if is_polyglot value then Polyglot_Data value else
            if is_unresolved_symbol value then Unresolved_Symbol_Data value else
                if is_error value then Error_Data value.catch else
                    Primitive_Data value

## UNSTABLE
   ADVANCED

   Checks whether two objects are represented by the same underlying reference.

   Arguments:
   - value_1: The first value.
   - value_2: The second value.
is_same_object : Any -> Any -> Boolean
is_same_object value_1 value_2 = @Builtin_Method "Meta.is_same_object"

## UNSTABLE
   ADVANCED

   Checks whether `self` represents the same underlying reference as `value`.

   Arguments:
   - value_1: The first value.
   - value_2: The second value.
Any.is_same_object_as : Any -> Boolean
Any.is_same_object_as self value = is_same_object self value

## UNSTABLE
   ADVANCED

   Checks if `self` is an instance of `typ`.

   Arguments:
   - typ: The type to check `self` against.
Any.is_a : Any -> Boolean
Any.is_a self typ = is_a self typ

## UNSTABLE
   ADVANCED

   Checks if `self` is an instance of `typ`.

   Arguments:
   - typ: The type to check `self` against.
Base.Error.is_a : Any -> Boolean
Base.Error.is_a self typ = typ==Any || typ==Base.Error

## UNSTABLE
   ADVANCED

   Checks if `value` is an instance of `typ`.

   Arguments:
   - value: The value to check for being an instance of `typ`.
   - typ: The type to check `self` against.
is_a : Any -> Any -> Boolean
is_a value typ = if is_same_object value typ then True else
    if typ == Any then True else
        if is_error value then typ == Base.Error else
            case value of
                _ : Vector.Vector -> typ.is_same_object_as Vector.Vector
                _ : Array -> typ == Array
                _ : Boolean -> if typ == Boolean then True else value == typ
                _ : Text -> typ == Text
                _ : Number -> if typ == Number then True else case value of
                    _ : Integer -> typ == Integer
                    _ : Decimal -> typ == Decimal
                _ : Date_Time.Date_Time -> typ.is_same_object_as Date_Time.Date_Time
                _ : Date.Date      -> typ.is_same_object_as Date.Date
                _ : Duration.Duration -> typ.is_same_object_as Duration.Duration
                _ : Time_Of_Day.Time_Of_Day -> typ.is_same_object_as Time_Of_Day.Time_Of_Day
                _ : Time_Zone.Time_Zone -> typ.is_same_object_as Time_Zone.Time_Zone
                Base.Polyglot ->
                    typ==Base.Polyglot || java_instance_check value typ
                _ ->
                    meta_val = meta value
                    case meta_val of
                        Atom_Data _ -> if is_atom typ then typ == value else
                            meta_val.constructor == typ
                        Constructor_Data _ ->
                            meta_typ = meta typ
                            case meta_typ of
                                Atom_Data _ -> meta_val == meta_typ.constructor
                                Constructor_Data _ -> meta_val == meta_typ
                                _ -> False
                        Error_Data _ -> typ == Error
                        Unresolved_Symbol_Data _ -> typ == Unresolved_Symbol
                        _ -> False

## PRIVATE
java_instance_check value typ =
    val_java = get_polyglot_language value == "java"
    typ_java = get_polyglot_language typ == "java"
    val_java && typ_java && Base.Java.is_instance value typ

## UNSTABLE
   ADVANCED

   Returns the type of the given value.

   Arguments:
   - value: the value to get the type of.
type_of : Any -> Any
type_of value = @Builtin_Method "Meta.type_of_builtin"

# TODO Dubious constructor export
from project.Meta.Language import all
from project.Meta.Language export all

## Represents a polyglot language.
type Language

    ## UNSTABLE
       ADVANCED

       The Java language.
    Java

    ## UNSTABLE
       ADVANCED

       An unknown language.
    Unknown

## PRIVATE

   Checks if the provided value is an atom constructor.

   Arguments:
   - value: The value to check.
is_atom_constructor : Any -> Boolean
is_atom_constructor value = @Builtin_Method "Meta.is_atom_constructor"

## PRIVATE

   Checks if the provided value is an atom.

   Arguments:
   - value: The value to check.
is_atom : Any -> Boolean
is_atom value = @Builtin_Method "Meta.is_atom"

## PRIVATE

   Checks if the provided value is a runtime error.

   Arguments:
   - value: The value to check.
is_error : Any -> Boolean
is_error value = @Builtin_Method "Meta.is_error"

## PRIVATE

   Checks if the provided value is a polyglot value.

   Arguments:
   - value: The value to check.
is_polyglot : Any -> Boolean
is_polyglot value = @Builtin_Method "Meta.is_polyglot"

## PRIVATE

   Checks if the provided value is an unresolved symbol.

   Arguments:
   - value: The value to check.
is_unresolved_symbol : Any -> Boolean
is_unresolved_symbol value = @Builtin_Method "Meta.is_unresolved_symbol"

## PRIVATE

   Returns a Text representing the source location of a stack frame above
   the call.

   Arguments:
   - frames_to_skip: how many frames on the stack to skip. Called with 0
     will return exact location of the call.
get_source_location_builtin : Integer -> Text
get_source_location_builtin frames_to_skip = @Builtin_Method "Meta.get_source_location_builtin"

## PRIVATE

   Returns a Text representing the source location of a stack frame above
   the call.

   Arguments:
   - skip_frames: how many frames on the stack to skip.

   If the function is called with 0 it will return the location of that call.
   Note that not only function calls, but also things like pattern matching add
   frames to the stack and there is no guarantee that the amount of frames that
   need to be skipped will not change between versions, so this method should be
   used carefully.
get_source_location : Integer -> Text
get_source_location skip_frames =
    get_source_location_builtin skip_frames+1

## PRIVATE

   Displays the type of the provided value as text.

   Arguments:
   - value: The value for which to display the type.
get_simple_type_name : Any -> Text
get_simple_type_name value = @Builtin_Method "Meta.get_simple_type_name"

## PRIVATE

   Returns the fully qualified type name of the given value.

   Arguments:
   - value: the value to get the type of.
get_qualified_type_name : Any -> Text
get_qualified_type_name value = @Builtin_Method "Meta.get_qualified_type_name"
