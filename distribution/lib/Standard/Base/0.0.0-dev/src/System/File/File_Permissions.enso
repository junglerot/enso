import project.Data.Boolean.Boolean
import project.Data.Text.Text
import project.Data.Vector.Vector

polyglot java import java.nio.file.attribute.PosixFilePermission

type Permission
    ## Permission for read access for a given entity.
    Read

    ## Permission for write access for a given entity.
    Write

    ## Permission for execute access for a given entity.
    Execute

type File_Permissions
    ## PRIVATE
       Access permissions for a file.
    Value (owner : Vector Permission) (group : Vector Permission) (others : Vector Permission)

    ## PRIVATE
       ADVANCED
       Converts the Enso atom to its Java enum counterpart.
    to_java : Vector PosixFilePermission
    to_java self =
        result = Vector.new_builder
        if self.owner.contains Permission.Read then
            result.append PosixFilePermission.OWNER_READ
        if self.owner.contains Permission.Write then
            result.append PosixFilePermission.OWNER_WRITE
        if self.owner.contains Permission.Execute then
            result.append PosixFilePermission.OWNER_EXECUTE
        if self.group.contains Permission.Read then
            result.append PosixFilePermission.GROUP_READ
        if self.group.contains Permission.Write then
            result.append PosixFilePermission.GROUP_WRITE
        if self.group.contains Permission.Execute then
            result.append PosixFilePermission.GROUP_EXECUTE
        if self.others.contains Permission.Read then
            result.append PosixFilePermission.OTHERS_READ
        if self.others.contains Permission.Write then
            result.append PosixFilePermission.OTHERS_WRITE
        if self.others.contains Permission.Execute then
            result.append PosixFilePermission.OTHERS_EXECUTE
        result.to_vector

    ## GROUP Metadata
       Checks if the given file can be read by the owner.
    owner_read : Boolean
    owner_read self = self.owner.contains Permission.Read

    ## GROUP Metadata
       Checks if the given file can be written by the owner.
    owner_write : Boolean
    owner_write self = self.owner.contains Permission.Write

    ## GROUP Metadata
       Checks if the given file can be executed by the owner.
    owner_execute : Boolean
    owner_execute self = self.owner.contains Permission.Execute

    ## Checks if the given file can be read by the group.
    group_read : Boolean
    group_read self = self.group.contains Permission.Read

    ## Checks if the given file can be written by the group.
    group_write : Boolean
    group_write self = self.group.contains Permission.Write

    ## Checks if the given file can be executed by the group.
    group_execute : Boolean
    group_execute self = self.group.contains Permission.Execute

    ## GROUP Metadata
       Checks if the given file can be read by others.
    others_read : Boolean
    others_read self = self.others.contains Permission.Read

    ## GROUP Metadata
       Checks if the given file can be written by others.
    others_write : Boolean
    others_write self = self.others.contains Permission.Write

    ## GROUP Metadata
       Checks if the given file can be executed by others.
    others_execute : Boolean
    others_execute self = self.others.contains Permission.Execute

    ## PRIVATE
       ADVANCED
       Converts a Java `Set` of Java `PosixFilePermission` to `File_Permissions`.
    from_java_set java_set =
        owner = Vector.new_builder
        group = Vector.new_builder
        others = Vector.new_builder

        if java_set.contains PosixFilePermission.OWNER_READ then
            owner.append Permission.Read
        if java_set.contains PosixFilePermission.OWNER_WRITE then
            owner.append Permission.Write
        if java_set.contains PosixFilePermission.OWNER_EXECUTE then
            owner.append Permission.Execute
        if java_set.contains PosixFilePermission.GROUP_READ then
            group.append Permission.Read
        if java_set.contains PosixFilePermission.GROUP_WRITE then
            group.append Permission.Write
        if java_set.contains PosixFilePermission.GROUP_EXECUTE then
            group.append Permission.Execute
        if java_set.contains PosixFilePermission.OTHERS_READ then
            others.append Permission.Read
        if java_set.contains PosixFilePermission.OTHERS_WRITE then
            others.append Permission.Write
        if java_set.contains PosixFilePermission.OTHERS_EXECUTE then
            others.append Permission.Execute

        File_Permissions.Value owner.to_vector group.to_vector others.to_vector

    ## PRIVATE
       Convert to a display representation of this File_Permissions.
    to_display_text : Text
    to_display_text self =
        owner = "Owner: " + (if self.owner_read then "r" else "-") + (if self.owner_write then "w" else "-") + (if self.owner_execute then "x" else "-")
        group = "Group: " + (if self.group_read then "r" else "-") + (if self.group_write then "w" else "-") + (if self.group_execute then "x" else "-")
        other = "Other: " + (if self.others_read then "r" else "-") + (if self.others_write then "w" else "-") + (if self.others_execute then "x" else "-")
        owner + ", " + group + ", " + other
