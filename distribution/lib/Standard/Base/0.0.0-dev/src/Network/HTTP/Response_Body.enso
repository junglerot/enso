import project.Data.Boolean.Boolean
import project.Data.Json.Json
import project.Data.Json.JS_Object
import project.Data.Numbers.Number
import project.Data.Text.Extensions
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Nothing.Nothing
import project.System.File.File
import project.System.File.Write_Extensions

## PRIVATE
type Response_Body
    ## PRIVATE
       Response body

       Arguments:
       - bytes: The body of the response as binary data.
    Value bytes

    ## PRIVATE
       Convert response body to Text.
    to_text : Text
    to_text self = Text.from_utf_8 self.bytes

    ## Write response body to a File.

       Arguments:
       - file: The file to write the bytes to.

       > Examples
         Write the contents of the request body to a scratch file on disk. The
         file will be created if it does not exist, and will be overwritten if
         it does.

             import Standard.Examples

             example_to_file =
                Examples.get_geo_data.to_file Examples.scratch_file
    to_file : File -> File
    to_file self file =
        self.bytes.write_bytes file
        file

    ## Convert response body from JSON to the Enso data types.

       > Example
         Convert a response from JSON. NOTE: This example makes a network request.

             import Standard.Examples

             example_to_text = Examples.get_geo_data.parse_json
    parse_json : JS_Object | Boolean | Number | Nothing | Text | Vector
    parse_json self = Json.parse self.to_text
