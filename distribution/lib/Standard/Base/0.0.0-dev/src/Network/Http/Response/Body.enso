from Standard.Base import all

type Body

    ## Response body

       Arguments:
       - bytes: The body of the response as binary data.
    Body_Data bytes

    ## Convert response body to Text.

       > Example
         Convert a response to text. NOTE: This example makes a network request.

             import Standard.Examples

             example_to_text = Examples.get_geo_data.to_text
    to_text : Text
    to_text self = Text.from_utf_8 self.bytes

    ## Convert response body to Json.

       > Example
         Convert a response to JSON. NOTE: This example makes a network request.

             import Standard.Examples

             example_to_text = Examples.get_geo_data.to_json
    to_json : Json
    to_json self = Json.parse self.to_text

    ## Write response body to a File.

       Arguments:
       - file: The file to write the bytes to.

       > Examples
         Write the contents of the request body to a scratch file on disk. The
         file will be created if it does not exist, and will be overwritten if
         it does.

             import Standard.Examples

             example_to_file =
                Examples.get_geo_data.to_file Examples.scratch_file
    to_file : File -> File
    to_file self file =
        self.bytes.write_bytes file
        file

