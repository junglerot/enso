from Standard.Base import all
import Standard.Base.Runtime.Ref.Ref

import project.Internal.Vector_Builder.Vector_Builder

from project.Errors import Missing_Input_Columns, Column_Indexes_Out_Of_Range, Duplicate_Column_Selectors, Input_Indices_Already_Matched, Column_Matched_By_Multiple_Selectors, Duplicate_Output_Column_Names, Invalid_Output_Column_Names

type Problem_Builder
    Value oob_indices duplicate_column_selectors input_indices_already_matched missing_input_columns other

    report_oob_indices self indices =
        append_to_ref self.oob_indices indices

    report_duplicate_column_selectors self selectors =
        append_to_ref self.duplicate_column_selectors selectors

    report_input_indices_already_matched self indices =
        append_to_ref self.input_indices_already_matched indices

    report_missing_input_columns self columns =
        append_to_ref self.missing_input_columns columns

    report_column_matched_by_multiple_selectors self column_name selectors =
        self.report_other_warning (Column_Matched_By_Multiple_Selectors.Error column_name selectors)

    report_unique_name_strategy self unique_name_strategy =
        if unique_name_strategy.invalid_names.not_empty then
            self.report_other_warning (Invalid_Output_Column_Names.Error unique_name_strategy.invalid_names)
        if unique_name_strategy.renames.not_empty then
            self.report_other_warning (Duplicate_Output_Column_Names.Error unique_name_strategy.renames)

    report_other_warning self warning =
        self.other.append warning

    ## Returns a vector containing all reported problems, aggregated.
    build_problemset : Vector
    build_problemset self =
        problems = Vector.new_builder
        build_vector_and_append ref problem_creator =
            vec = ref.get . build
            if vec.not_empty then
                problems.append (problem_creator vec)

        build_vector_and_append self.oob_indices Column_Indexes_Out_Of_Range.Error
        build_vector_and_append self.duplicate_column_selectors Duplicate_Column_Selectors.Error
        build_vector_and_append self.input_indices_already_matched Input_Indices_Already_Matched.Error
        build_vector_and_append self.missing_input_columns Missing_Input_Columns.Error
        self.other.to_vector.each problems.append

        problems.to_vector

    ## If there are any missing columns or out of bound indices, the
       corresponding error is raised, otherwise, the provided action is executed
       and its result is returned.
    throw_unmatched_columns_as_error : Any -> Any
    throw_unmatched_columns_as_error self ~action =
         case self.missing_input_columns.get.not_empty of
            True ->
                err = Missing_Input_Columns.Error self.missing_input_columns.get.build
                Error.throw err
            False -> case self.oob_indices.get.not_empty of
                True ->
                    err = Column_Indexes_Out_Of_Range.Error self.oob_indices.get.build
                    Error.throw err
                False -> action

    ## Attaches gathered warnings to the result.
       Any errors from the `result` take precedence over the ones owned by this
       builder.
    attach_problems_after : Problem_Behavior -> Any -> Any
    attach_problems_after self problem_behavior result =
        problem_behavior.attach_problems_after result self.build_problemset

    ## Attaches gathered warnings to the result of the provided computation.
       If in `Report_Error` mode and there are any problems gathered, the first
       one will be returned as error without even running the computation.
    attach_problems_before : Problem_Behavior -> Any -> Any
    attach_problems_before self problem_behavior ~computation =
        problem_behavior.attach_problems_before self.build_problemset computation

    ## PRIVATE
       Creates a new helper object for aggregating problems to report.
    new : Problem_Builder
    new =
        Problem_Builder.Value (Ref.new Vector_Builder.empty) (Ref.new Vector_Builder.empty) (Ref.new Vector_Builder.empty) (Ref.new Vector_Builder.empty) other=Vector.new_builder

## PRIVATE
   Appends a `Vector` to a `Vector_Builder` stored in a `Ref`.
append_to_ref : Ref Vector_Builder -> Vector -> Nothing
append_to_ref ref vector =
    appended = (ref.get) ++ vector
    ref . put appended
