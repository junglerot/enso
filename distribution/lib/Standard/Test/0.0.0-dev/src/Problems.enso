from Standard.Base import all
import Standard.Test

import Standard.Base.Error.Problem_Behavior
import Standard.Base.Warning

## UNSTABLE
   Tests how a specific operation behaves depending on the requested
   `Problem_Behavior`.

   Arguments:
   - action: The action to execute. It takes a `Problem_Behavior` which
     specifies whether it should ignore problems, report them as warnings or
     raise a dataflow error on the first encountered problem.
   - expected_problems: a list of expected problems, in the order that they are
     expected to be reported. It should not be empty. The problems are assumed
     to be Atoms.
   - result_checker: A function which should verify that the result generated by
     the action is correct. It does not return anything, instead it should use
     the standard testing approach, like `x.should_equal y`.
test_problem_handling : (Problem_Behavior -> Any) -> Vector Any -> (Any -> Nothing) -> Nothing
test_problem_handling action expected_problems result_checker =
    # First, we check the action ignoring any warnings.
    result_ignoring = action Problem_Behavior.Ignore
    result_checker result_ignoring
    Warning.get_all result_ignoring . should_equal [] frames_to_skip=1

    # Then, we check the fail-on-first-error mode.
    first_problem = expected_problems.first
    first_problem_type = Meta.meta first_problem . constructor
    error_result = action Problem_Behavior.Report_Error
    error_result . should_fail_with first_problem_type frames_to_skip=1
    error_result.catch . should_equal first_problem frames_to_skip=1

    # Lastly, we check the report warnings mode and ensure that both the result is correct and the warnings are as expected.
    result_warning = action Problem_Behavior.Report_Warning
    result_checker result_warning
    warnings = Warning.get_all result_warning . map .value
    ## TODO [RW] we are not checking if there are no duplicate warnings, because the warnings are in fact duplicated - we should figure out how to handle that and then possibly modify the test
    warnings . should_contain_the_same_elements_as expected_problems frames_to_skip=1
