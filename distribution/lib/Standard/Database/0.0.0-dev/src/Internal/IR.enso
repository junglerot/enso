from Standard.Base import all
from Standard.Database.Data.SQL import SQL_Type

## PRIVATE

   The internal representation of an SQL expression which can be a column
   reference, an interpolated constant or an operation that combines other
   expressions.
type Expression

    ## PRIVATE

       The internal representation of an SQL expression that gets a value from a
       column.

       Arguments:
       - origin: the name of the entity (table or sub-query) that this column
         originates from, it corresponds to the `alias` field in `from_spec`.
       - name: the name of the column directly in the table or its alias in a
         sub-query.
    Column (origin : Text) (name : Text)

    ## PRIVATE

       The internal representation of an SQL expression which is a constant to
       be interpolated when building the query.

       Arguments:
       - sql_type: The SQL type that this object is going to be serialized to.
         It is usually inferred from the expression's context.
       - value: the value to be interpolated; it should be a simple Number, Text
         or other types that are serializable for JDBC.
    Constant (sql_type : SQL_Type) (value : Any)

    ## PRIVATE

       The internal representation of an SQL expression built from an operation
       and sub-expressions.

       Arguments:
       - kind: the name of the operation, these can be both functions or infix
         operators, the actual implementation is determined by a specific
         dialect.
       - expressions: a list of expressions which are arguments to the operation
         different operations support different amounts of arguments.
    Operation (kind : Text) (expressions : Vector Expression)

type Internal_Column
    ## PRIVATE

       An internal column structure.

       Arguments:
       - name: The column name.
       - sql_type: The SQL type of the column.
       - expression: An expression for applying to the column.
    Internal_Column_Data name sql_type expression

    ## PRIVATE

       Rename the internal column.

       Arguments:
       - new_name: The new name for the column.
    rename : Text -> Internal_Column
    rename self new_name = Internal_Column_Data new_name self.sql_type self.expression

## PRIVATE

   A context associated with an SQL query.
type Context

    ## PRIVATE

       A context associated with an SQL query.

       The expressions can only be computed in a context which specifies from where
       their columns come and set filters and other settings for processing the
       query.

        Arguments:
       - from_spec: the sources for the query, see `From_Spec` for more
         details.
       - where_filters: a list of expressions for filtering - only the rows
         for which these expressions evaluate to true are included in the
         result.
       - orders: a list of ordering expressions, for each entry an ORDER BY
         clause is added.
       - groups: a list of grouping expressions, for each entry a GROUP BY is
         added, the resulting query can then directly include only the
         grouped-by columns or aggregate expressions.
       - meta_index: a list of internal columns to use for joining or grouping.
       - limit: an optional maximum number of elements that the equery should
         return.
    Context_Data (from_spec : From_Spec) (where_filters : Vector Expression) (orders : Vector Order_Descriptor) (groups : Vector Expression) (meta_index : Vector Internal_Column) (limit : Nothing | Integer)

    ## PRIVATE

       Returns a copy of the context with changed `meta_index`.

       Arguments:
       - new_index: The new index to set in the query.
    set_index : Vector Internal_Column -> Context
    set_index self new_index =
        Context_Data self.from_spec self.where_filters self.orders self.groups new_index self.limit

    ## PRIVATE

       Returns a copy of the context with changed `where_filters`.

       Arguments:
       - new_filters: The new filters to set in the query.
    set_where_filters : Vector Expression -> Context
    set_where_filters self new_filters =
        Context_Data self.from_spec new_filters self.orders self.groups self.meta_index self.limit

    ## PRIVATE

       Returns a copy of the context with changed `orders`.

       Arguments:
       - new_orders: The new ordering clauses to set in the query.
    set_orders : Vector Order_Descriptor -> Context
    set_orders self new_orders =
        Context_Data self.from_spec self.where_filters new_orders self.groups self.meta_index self.limit

    ## PRIVATE

       Returns a copy of the context with added `orders`.

       The orderings are modified in such a way that the newly added ordering
       takes precedence, but if any orderings were already present they are also
       taken into account to break ties within the new ordering.

       In practice this means, that the old orderings are preserved, but the new
       ones are added to the beginning of the list so that they take precedence.

       Arguments:
       - new_orders: The new ordering clauses to add to the query.
    add_orders : Vector Order_Descriptor -> Context
    add_orders self new_orders =
        Context_Data self.from_spec self.where_filters new_orders+self.orders self.groups self.meta_index self.limit

    ## PRIVATE

       Returns a copy of the context with changed `groups`.

       Arguments:
       - new_groups: The new grouping clauses to set in the query.
    set_groups : Vector Expression -> Context
    set_groups self new_groups =
        Context_Data self.from_spec self.where_filters self.orders new_groups self.meta_index self.limit

    ## PRIVATE

       Returns a copy of the context with changed `limit`.

       Arguments:
       - new_limit: The new limit clauses to set in the query.
    set_limit : (Nothing | Integer) -> Context
    set_limit self new_limit =
       Context_Data self.from_spec self.where_filters self.orders self.groups self.meta_index new_limit

    ## PRIVATE

       'Lifts' this context into a subquery, so that the original context (with all filters etc.) is
       encapsulated within the subquery and all external references passed as the second argument,
       refer directly to that subquery.

       It takes a list of lists of columns that should be included in that subquery (this can for
       example the list of regular columns, the list of indices etc.)
       It assumes that columns on these lists all have unique names.

       It returns a new context and the lists transformed in such a way that each column corresponds
       to one from the original list but it is valid in the new context.

       This is useful as a preprocessing step between combining queries, for example in a join.
    # as_subquery : Text -> Vector (Vector Internal_Column) -> [IR.Sub_Query, Vector (Vector Internal_Column)]
    as_subquery : Text -> Vector Any -> Vector
    as_subquery self alias column_lists =
        rewrite_internal_column : Internal_Column -> Internal_Column
        rewrite_internal_column column =
            Internal_Column_Data column.name column.sql_type (IR.Column alias column.name)

        new_columns = column_lists.map columns->
            columns.map rewrite_internal_column

        encapsulated_columns = column_lists.flat_map columns->
            columns.map column-> [column.name, column.expression]
        new_from = IR.Sub_Query encapsulated_columns self alias

        [new_from, new_columns]


## PRIVATE

   Used as part of the context, specifies the sources of the query.
type From_Spec
    ## PRIVATE

       A query source that takes data directly from a table in the
       database.

       Arguments:
       - table_name: the full name identifying the table in the database.
       - alias: the name upon which the table can be referred to in other
         parts of the query, this is especially useful for example in
         self-joins, allowing to differentiate between different instances of
         the same table.
    From_Table (table_name : Text) (alias : Text)

    ## PRIVATE

       A query source that holds a raw SQL statement.

       Arguments:
       - raw_sql: the raw SQL code of a query used as a source for this table.
       - alias: the name upon which the table can be referred to in other
         parts of the query, this is especially useful for example in
         self-joins, allowing to differentiate between different instances of
         the same table.
    From_Query (raw_sql : Text) (alias : Text)

    ## PRIVATE

       A query source that performs a join operation on two sources.

       Arguments:
       - kind: the kind of the join.
       - left_spec: the left data source to be joined
       - right_spec: the right data source to be joined
       - on: a list of expressions that will be used as join conditions, these
         are usually be equalities between expressions from the left and right
         sources.
    Join (kind : Join_Kind) (left_spec : From_Spec) (right_spec : From_Spec) (on : Vector Expression)

    ## PRIVATE

       A query source consisting of a sub-query.

       Arguments:
       - columns: a list of pairs; each first element is the alias for the
         column created in the sub-query and the second element is the
         expression to be computed.
       - context: the context for the sub-query.
       - alias: the name upon which the results of this sub-query can be
         referred to in other parts of the query.
    Sub_Query (columns : Vector (Pair Text Expression)) (context : Context) (alias : Text)

## PRIVATE

   The kind of the join operation.
type Join_Kind
    ## PRIVATE

       Inner join.

       The result will contain only rows that had a match in both left and right
       source.
    Join_Inner

    ## PRIVATE

       Left join.

       The result will contain all rows from the left source. Some of them may
       be duplicated if they have multiple matches on the right. If a row from
       the left source has no match on the right, it will be present exactly
       once in the result and the fields corresponding to the right source will
       be set to NULL.
    Join_Left

    ## PRIVATE

       Right join.

       The result will contain all rows from the right source. Some of them may
       be duplicated if they have multiple matches on the left. If a row from
       the right source has no match on the left, it will be present exactly
       once in the result and the fields corresponding to the left source will
       be set to NULL.
    Join_Right

    ## PRIVATE

       Cross join.

       The result will contain a cross product of rows from the left source with
       the right source. Its `on` list should be empty, instead `where_filters`
       in the query can be used to filter the results.
    Join_Cross

## PRIVATE
type Order_Descriptor
    Order_Descriptor_Data (expression : Expression) (direction : Sort_Direction) (nulls_order : Nothing | Nulls_Order = Nothing) (collation : Nothing | Text = Nothing)

## PRIVATE

   Specifies how null values should be handled within the ordering.
type Nulls_Order

    ## PRIVATE

       Null values are included before any other values in the ordering.
    Nulls_First

    ## PRIVATE

       Null values are included after all other values in the ordering.
    Nulls_Last

## PRIVATE

   Describes a full SQL query.
type Query

    ## PRIVATE

       A Select SQL query.

       Arguments:
       - expressions: List of pairs specifying the columns to materialize; each
         is a pair whose first element is the name of the materialized column
         and the second element is the expression to compute.
       - context: The query context, see `Context` for more detail.
    Select (expressions : Vector (Pair Text Expression)) (context : Context)

    ## PRIVATE

       A Select SQL query that gets all columns in a table.

       Arguments:
       - context: The query context, see `Context` for more detail.
    Select_All context

    ## PRIVATE

       An Insert SQL query that inserts a single row to the table.

       Arguments:
       - table_name: The name of the table to insert to.
       - pairs: A list of pairs consisting of a column name and and expression.
    Insert table_name pairs

## PRIVATE

   Creates a query context that just fetches data from a table, without any
   additional processing.

   Arguments:
   - table_name: The name of the table for which the context is being created.
   - alias: An alias name to use for table within the query.
context_for_table : Text -> Text -> Context
context_for_table table_name alias=table_name =
    Context_Data (From_Table table_name alias) [] [] [] [] Nothing

## PRIVATE

   Creates a query context that runs a raw SQL query.

   Arguments:
   - raw_sql: The name of the table for which the context is being created.
   - alias: An alias name to use for table within the query.
context_for_query : Text -> Text -> Context
context_for_query raw_sql alias =
    Context_Data (From_Query raw_sql alias) [] [] [] [] Nothing

## PRIVATE

   Creates a Context from a Sub_Query.

   Arguments:
   - subquery: The subquery to lift into a context.
subquery_as_ctx : Sub_Query -> Context
subquery_as_ctx subquery =
    Context_Data subquery [] [] [] [] Nothing

## PRIVATE

   Creates an expression which is a simple constant to be interpolated.

   Arguments:
   - sql_type: The SQL type of the value.
   - x: The value to turn into a constant.
make_constant : SQL_Type -> Any -> Expression
make_constant sql_type x =
    Constant sql_type x

## PRIVATE

   A helper function to subsitute names tables inside of expressions.

   Arguments:
   - old_origin: The old table name.
   - new_origin: The new table name.
   - expr: The expression in which the substitution should be performed.

   It is used for example when renaming a table during a join.
substitute_origin : Text -> Text -> Expression -> Expression
substitute_origin old_origin new_origin expr = case expr of
    Column origin name ->
        if origin == old_origin then Column new_origin name else expr
    Constant _ _ -> expr
    Operation kind exprs ->
        Operation kind (exprs.map (substitute_origin old_origin new_origin))

## PRIVATE

   Lifts a function mapping expressions into a function mapping internal columns
   which applies the original function to their expressions, leaving other
   fields as-is.

   Arguments:
   - f: The function to map over expressions.
   - col: The column over which to apply `f`.
lift_expression_map : (Expression -> Expression) -> Internal_Column -> Internal_Column
lift_expression_map f col =
    Internal_Column_Data col.name col.sql_type (f col.expression)

