from Standard.Base import all
import Standard.Base.Error.Illegal_Argument.Illegal_Argument
import Standard.Base.Error.Illegal_State.Illegal_State

from Standard.Table import Sort_Column, Data_Formatter
import Standard.Table.Data.Value_Type.Value_Type
import Standard.Table.Data.Column.Column as Materialized_Column
import Standard.Table.Internal.Java_Problems
import Standard.Table.Internal.Problem_Builder.Problem_Builder
import Standard.Table.Internal.Widget_Helpers
from Standard.Table.Data.Value_Type import Value_Type, Auto
from Standard.Table.Errors import Floating_Point_Equality

import project.Data.SQL_Statement.SQL_Statement
import project.Data.SQL_Type.SQL_Type
import project.Internal.Helpers
import project.Internal.IR.Context.Context
import project.Internal.IR.SQL_Expression.SQL_Expression
import project.Internal.IR.Internal_Column.Internal_Column
import project.Internal.IR.Query.Query

from project.Data.Table import Table, freshen_columns

from project.Errors import Unsupported_Database_Operation, Integrity_Error, Unsupported_Name

polyglot java import org.enso.table.data.column.operation.map.MapOperationProblemBuilder

type Column

    ## UNSTABLE
       PRIVATE

       Represents a single column backed by a database.

       Arguments:
       - name: The name of the column.
       - connection: The connection with which the column is associated.
       - expression: The expressions to apply to the column.
       - context: The SQl context in which the column exists.

       These columns may come from the Table or can be created by combining
       other columns with operators. Expressions created in this way may be
       materialized or used to apply filters, groupings etc. to tables from
       which they come. Combined expressions must come from the same context -
       they must both have the same filtering, grouping etc. rules applied to be
       able to be combined.
    Value name:Text connection:Connection sql_type:SQL_Type expression:SQL_Expression context:Context

    ## UNSTABLE

       Returns a text containing an ASCII-art table displaying this data.

       Arguments:
         - show_rows: the number of initial rows that should be displayed.
         - format_terminal: whether ANSI-terminal formatting should be used
    display : Integer -> Boolean -> Text
    display self show_rows=10 format_terminal=False =
        self.to_table.display show_rows format_terminal

    ## UNSTABLE

       Prints an ASCII-art table with this data to the standard output.

       Arguments:
         - show_rows: the number of initial rows that should be displayed.
    print : Nothing
    print self show_rows=10 =
        IO.println (self.display show_rows format_terminal=True)
        IO.println ''

    ## UNSTABLE

       Converts this column to JS_Object representation.
    to_js_object : JS_Object
    to_js_object self = self.to_sql.to_js_object

    ## UNSTABLE

       Converts this column into a single-column table.
    to_table : Table
    to_table self =
        Table.Value self.name self.connection [self.as_internal] self.context

    ## UNSTABLE

       Returns a materialized column containing rows of this column.

       Arguments:
       - max_rows: specifies a maximum amount of rows to fetch; if not set, all
         available rows are fetched.
    read : (Nothing | Integer) -> Materialized_Column
    read self max_rows=Nothing =
        self.to_table.read max_rows . at self.name

    ## UNSTABLE

       Returns a vector containing all the elements in this column.
    to_vector : Vector Any
    to_vector self =
        self.to_table.read . at self.name . to_vector

    ## UNSTABLE TODO this is a very early prototype that will be revisited later
       This implementation is really just so that we can use the types in
       `filter`, it does not provide even a decent approximation of the true
       type in many cases. It will be improved when the types work is
       implemented.
    value_type : Value_Type
    value_type self =
        ## TODO This is a temporary implementation that should be revised once
           types are implemented properly.
        if self.sql_type.is_definitely_boolean then Value_Type.Boolean else
            if self.sql_type.is_definitely_text then Value_Type.Char else
                if self.sql_type.is_definitely_double then Value_Type.Float else
                    Value_Type.Unsupported_Data_Type self.sql_type.name
    ## UNSTABLE

       Returns an SQL statement that will be used for materializing this column.
    to_sql : SQL_Statement
    to_sql self = self.to_table.to_sql

    ## PRIVATE
       Sets up an operation of arbitrary arity.

       Arguments:
       - op_kind: The kind of the operation
       - operands: A vector of additional operation arguments (the column itself
         is always passed as the first argument).
       - new_type: The type of the SQL column that results from applying the
         operator. If not specified, the type of this column is used.
       - operand_types: The SQL types of the additional arguments. They are used
         if additional arguments are constants (and if not provided, the type of
         this column is used). If the other argument is a column, its type is
         used.
    make_op self op_kind operands new_type=Nothing operand_types=Nothing =
        prepare_operand operand operand_type = case operand of
            other_column : Column ->
                if Helpers.check_integrity self other_column then other_column.expression else
                    Error.throw <| Unsupported_Database_Operation.Error "Cannot use columns coming from different contexts in one expression without a join."
            constant ->
                actual_operand_type = operand_type.if_nothing self.sql_type
                SQL_Expression.Constant actual_operand_type constant

        actual_operand_types = operand_types.if_nothing (Vector.fill operands.length Nothing)
        expressions = operands.zip actual_operand_types prepare_operand

        actual_new_type = new_type.if_nothing self.sql_type
        new_expr = SQL_Expression.Operation op_kind ([self.expression] + expressions)
        Column.Value self.name self.connection actual_new_type new_expr self.context

    ## PRIVATE

       Creates a binary operation with given kind and operand.

       Arguments:
       - op_kind: The kind of binary operator.
       - operand: The right operand to the binary operator.
       - new_type: The type of the SQL column that results from applying the
         operator.
       - operand_type: The SQL type of the operand.

       If not specified, the `new_type` is the same as the current one.
       `operand_type` is only relevant if the operand is not a column, it
       defaults to the current type if not provided.
    make_binary_op : Text -> Text -> (Column | Any) -> (SQL_Type | Nothing) -> (SQL_Type | Nothing) -> Column
    make_binary_op self op_kind operand new_type=Nothing operand_type=Nothing =
        self.make_op op_kind [operand] new_type [operand_type]

    ## PRIVATE

       Helper for implementing unary operators.

       Arguments:
       - op_kind: The kind of the unary operator.
       - new_type: The type of the SQL column that results from applying the
         operator.
    make_unary_op : Text -> Text -> (SQL_Type | Nothing) -> Column
    make_unary_op self op_kind new_type=Nothing = self.make_op op_kind [] new_type

    ## UNSTABLE

       Returns the length of this column.
    length : Integer
    length self = self.to_table.row_count

    ## UNSTABLE

       Returns the number of missing items in this column.
    count_missing : Integer
    count_missing self =
        self.to_table.filter 0 Filter_Condition.Is_Nothing . row_count

    ## UNSTABLE

       Returns the number of non-null items in this column.
    count : Integer
    count self =
        self.to_table.filter 0 Filter_Condition.Not_Nothing . row_count

    ## Element-wise equality comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       ! Warnings

         - If this operation results in comparing floating-point values for
           equality which is not recommended, a `Floating_Point_Equality`
           warning is attached to the result.

       > Example
         Compare two columns for pairwise equality.

             import Standard.Examples

             example_eq = Examples.integer_column == Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_eq = Examples.integer_column == 1
    == : Column | Any -> Column
    == self other =
        make_equality_check_with_floating_point_handling self other "=="

    ## Element-wise case-insensitive text equality comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.
    equals_ignore_case : Column | Any -> Locale -> Column
    equals_ignore_case self other locale=Locale.default =
        Helpers.assume_default_locale locale <|
            self.make_binary_op "equals_ignore_case" other new_type=SQL_Type.boolean

    ## Element-wise non-equality comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       ! Warnings

         - If this operation results in comparing floating-point values for
           equality which is not recommended, a `Floating_Point_Equality`
           warning is attached to the result.

       > Example
         Compare two columns for pairwise inequality.

             import Standard.Examples

             example_neq = Examples.integer_column != Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_neq = Examples.integer_column != 1
    != : Column | Any -> Column
    != self other =
        make_equality_check_with_floating_point_handling self other "!="

    ## UNSTABLE

       Element-wise order comparison.

       Arguments:
       - other: The other column to compare pairwise with.

       Returns a column with results of comparing this column's elements against
       `other`.  If `other` is a column, the comparison is performed pairwise
       between corresponding elements of `self` and `other`.
    >= : Column | Any -> Column
    >= self other = self.make_binary_op ">=" other new_type=SQL_Type.boolean

    ## UNSTABLE

       Element-wise order comparison.

       Arguments:
       - other: The other column to compare pairwise with.

       Returns a column with results of comparing this column's elements against
       `other`.  If `other` is a column, the comparison is performed pairwise
       between corresponding elements of `self` and `other`.
    <= : Column | Any -> Column
    <= self other = self.make_binary_op "<=" other new_type=SQL_Type.boolean

    ## UNSTABLE

       Element-wise order comparison.

       Arguments:
       - other: The other column to compare pairwise with.

       Returns a column with results of comparing this column's elements against
       `other`.  If `other` is a column, the comparison is performed pairwise
       between corresponding elements of `self` and `other`.
    > : Column | Any -> Column
    > self other = self.make_binary_op ">" other new_type=SQL_Type.boolean

    ## UNSTABLE

       Element-wise order comparison.

       Arguments:
       - other: The other column to compare pairwise with.

       Returns a column with results of comparing this column's elements against
       `other`.  If `other` is a column, the comparison is performed pairwise
       between corresponding elements of `self` and `other`.
    < : Column | Any -> Column
    < self other = self.make_binary_op "<" other new_type=SQL_Type.boolean

    ## Element-wise inclusive bounds check.

       Arguments:
       - lower: The lower bound to compare elements of `self` against. If
         `lower` is a column, the comparison is performed pairwise between
         corresponding elements of `self` and `lower`.
       - upper: The upper bound to compare elements of `self` against. If
         `upper` is a column, the comparison is performed pairwise between
         corresponding elements of `self` and `upper`.

       Returns a column with boolean values indicating whether values of this
       column fit between the lower and upper bounds (both ends inclusive).
    between : (Column | Any) -> (Column | Any) -> Column
    between self lower upper =
        self.make_op "BETWEEN" [lower, upper] new_type=SQL_Type.boolean

    ## UNSTABLE

       Element-wise addition.

       Arguments:
       - other: The other column to add to this column.

       Returns a column containing the result of adding `other` to each element
       of `self`.  If `other` is a column, the operation is performed pairwise
       between corresponding elements of `self` and `other`.
    + : Column | Any -> Column
    + self other =
        ## TODO: Revisit this as part of the column value type work.
        op = case other of
            _ : Column -> if self.sql_type.is_definitely_numeric || other.sql_type.is_definitely_numeric then 'ADD_NUMBER' else 'ADD_TEXT'
            _ -> if self.sql_type.is_definitely_numeric then 'ADD_NUMBER' else 'ADD_TEXT'
        new_type = if op == 'ADD_TEXT' then self.sql_type else
            SQL_Type.merge_type self.sql_type (SQL_Type.approximate_type other)
        self.make_binary_op op other new_type=new_type

    ## UNSTABLE

       Element-wise subtraction.

       Arguments:
       - other: The other column to subtract from this column.

       Returns a column containing the result of subtracting `other` from each
       element of `self`.  If `other` is a column, the operation is performed
       pairwise between corresponding elements of `self` and `other`.
    - : Column | Any -> Column
    - self other =
        new_type = SQL_Type.merge_type self.sql_type (SQL_Type.approximate_type other)
        self.make_binary_op "-" other new_type=new_type

    ## UNSTABLE

       Element-wise multiplication.

       Arguments:
       - other: The other column to multiply `self` by.

       Returns a column containing the result of multiplying `other` by each
       element of `self`.  If `other` is a column, the operation is performed
       pairwise between corresponding elements of `self` and `other`.
    * : Column | Any -> Column
    * self other =
        new_type = SQL_Type.merge_type self.sql_type (SQL_Type.approximate_type other)
        self.make_binary_op "*" other new_type=new_type

    ## ALIAS Divide Columns

       Element-wise division.

       Arguments:
       - other: The value to divide `self` by. If `other` is a column, the
         division is performed pairwise between corresponding elements of `self`
         and `other`.

       Returns a column containing the result of dividing each element of `self`
       by `other`.

       ! Warnings

         - If division by zero occurs, an `Arithmetic_Error` warning is attached
           to the result.

       > Example
         Divide the elements of one column by the elements of another.

             import Standard.Examples

             example_div = Examples.decimal_column / Examples.integer_column

       > Example
         Multiply each value in a column by a single value.

             import Standard.Examples

             example_div = Examples.integer_column / 10
    / : Column | Any -> Column
    / self other =
        self.make_binary_op "/" other new_type=SQL_Type.double

    ## Element-wise modulus.

       Arguments:
       - other: The value to modulo `self` against. If `other` is a column, the
         modulus is performed pairwise between corresponding elements of `self`
         and `other`.

       Returns a column with results of modulus this column's elements against
       `other`.

       ! Warnings

         - If division by zero occurs, an `Arithmetic_Error` warning is attached
           to the result.

       > Example
         Modulus of two columns against each other.

             import Standard.Examples

             example_mod = Examples.integer_column % Examples.decimal_column

       > Example
         Modulus of a column with a number.

             import Standard.Examples

             example_mod = Examples.integer_column % 3
    % : Column | Any -> Column
    % self other =
        new_type = SQL_Type.merge_type self.sql_type (SQL_Type.approximate_type other)
        op = if new_type == SQL_Type.integer then "%" else "mod"
        self.make_binary_op op other new_type=new_type

    ## ALIAS Power

       Element-wise raising to the power.

       Arguments:
       - other: The exponent to raise `self` by. If `other` is a column, the
         power operation is performed pairwise between corresponding elements
         of `self` and `other`.

       Returns a column containing the result of raising each element of `self`
       by `other`.

       > Example
         Squares the elements of one column.

             import Standard.Examples

             example_div = Examples.decimal_column ^ 2

       > Example
         Raises each value in a column by the value in another column.

             import Standard.Examples

             example_div = Examples.decimal_column ^ Examples.integer_column
    ^ : Column | Any -> Column
    ^ self other =
        self.make_binary_op '^' other new_type=SQL_Type.double

    ## UNSTABLE

       Element-wise boolean conjunction.

       Arguments:
       - other: The other column to compute the conjunction with.

       Returns a column containing the result of performing the boolean `and` on
       `other` and each element of `self`.  If `other` is a column, the
       operation is performed pairwise between corresponding elements of `self`
       and `other`.
    && : Column | Any -> Column
    && self other = self.make_binary_op "AND" other new_type=SQL_Type.boolean

    ## UNSTABLE

       Element-wise boolean disjunction.

       Arguments:
       - other: The other column to compute the disjunction with.

       Returns a column containing the result of performing the boolean `or` on
       `other` and each element of `self`.  If `other` is a column, the
       operation is performed pairwise between corresponding elements of `self`
       and `other`.
    || : Column | Any -> Column
    || self other = self.make_binary_op "OR" other new_type=SQL_Type.boolean

    ## UNSTABLE

       Boolean negation of each element in this column.
    not : Column
    not self = self.make_unary_op "NOT"

    ## ALIAS IF

       Replaces `True` values with `when_true` and `False` with `when_false`.
       Only meant for use with boolean columns.

       Arguments:
       - when_true: value or column when `self` is `True`.
       - when_false: value or column when `self` is `False`.
    iif : Any -> Any -> Column
    iif self when_true when_false =
        ## TODO we should adjust new_type based on types when_true and
           when_false, but this relies on the Value Types design which is still
           in progress. This function has status of an internal prototype for
           now, so we just rely on a simplified handling. Once Value Types are
           properly implemented, this should be accordingly extended for the
           full implementation of IIF. We will need to handle when_true and
           when_false being either columns or regular values and rely on a
           mapping of Enso base types to SQL types, and a rule for extracting a
           common type.
        left_type = get_approximate_type when_true self.sql_type
        right_type = get_approximate_type when_false self.sql_type
        new_type = SQL_Type.merge_type left_type right_type

        if new_type.is_error then Error.throw (Illegal_Argument.Error "when_true and when_false types do not match") else
            self.make_op "IIF" [when_true, when_false] new_type=new_type

    ## Returns a column of first non-`Nothing` value on each row of `self` and
       `values` list.

       Arguments:
       - values: list of columns or values to coalesce with `self`.

       > Example
         Get the first non-`Nothing` value in two columns.

             import Standard.Examples

             example_coalesce = Examples.decimal_column.coalesce Examples.integer_column
    coalesce : (Any | Vector Any) -> Column
    coalesce self values = case values of
        _ : Vector ->
            fold_type = values.fold self.sql_type c->v-> SQL_Type.merge_type c (get_approximate_type v self.sql_type)
            if fold_type.is_error then Error.throw (Illegal_Argument.Error "self and values types do not all match") else
                self.make_op "COALESCE" values new_type=fold_type
        _ : Array -> self.coalesce (Vector.from_polyglot_array values)
        _ -> self.coalesce [values]

    ## Returns a column of minimum on each row of `self` and `values` list.

       Arguments:
       - values: list of columns or values to minimum with `self`.

       > Example
         Get the minimum value in two columns.

             import Standard.Examples

             example_min = Examples.decimal_column.min Examples.integer_column
    min : (Any | Vector Any) -> Column
    min self values = case values of
        _ : Vector ->
            fold_type = values.fold self.sql_type c->v-> SQL_Type.merge_type c (get_approximate_type v self.sql_type)
            if fold_type.is_error then Error.throw (Illegal_Argument.Error "self and values types do not all match") else
                self.make_op "ROW_MIN" values new_type=fold_type
        _ : Array -> self.min (Vector.from_polyglot_array values)
        _ -> self.min [values]

    ## Returns a column of maximum on each row of `self` and `values` list.

       Arguments:
       - values: list of columns or values to maximum with `self`.

       > Example
         Get the maximum value in two columns.

             import Standard.Examples

             example_max = Examples.decimal_column.max Examples.integer_column
    max : (Any | Vector Any) -> Column
    max self values = case values of
        _ : Vector ->
            fold_type = values.fold self.sql_type c->v-> SQL_Type.merge_type c (get_approximate_type v self.sql_type)
            if fold_type.is_error then Error.throw (Illegal_Argument.Error "self and values types do not all match") else
                self.make_op "ROW_MAX" values new_type=fold_type
        _ : Array -> self.max (Vector.from_polyglot_array values)
        _ -> self.max [values]

    ## UNSTABLE

       Returns a column of booleans, with `True` items at the positions where
       this column contains a `Nothing`.
    is_missing : Column
    is_missing self = self.make_unary_op "IS_NULL" new_type=SQL_Type.boolean

    ## UNSTABLE
       Returns a column of booleans, with `True` items at the positions where
       this column contains a NaN. This is only applicable to double columns.
    is_nan : Column
    is_nan self = self.make_unary_op "IS_NAN" new_type=SQL_Type.boolean

    ## PRIVATE
       Returns a column of booleans, with `True` items at the positions where
       this column contains an empty string or `Nothing`.
    is_empty : Column
    is_empty self = self.make_unary_op "IS_EMPTY" new_type=SQL_Type.boolean

    ## Returns a column of booleans, with `True` items at the positions where
       this column does not contain a `Nothing`.

       > Example
         Check a column for present values.

             import Standard.Examples

             example_is_present = Examples.decimal_column.is_present
    is_present : Column
    is_present self = self.is_missing.not

    ## PRIVATE
       Returns a column of booleans with `True` at the positions where this
       column contains a blank value.

       Arguments:
       - treat_nans_as_blank: If `True`, then `Number.nan` is considered as
         blank.

       ? Blank values
         Blank values are `Nothing`, `""` and depending on setting `Number.nan`.
    is_blank : Boolean -> Boolean -> Column
    is_blank self treat_nans_as_blank=False =
        is_blank = case self.sql_type.is_definitely_text of
            True -> self.is_empty
            False -> self.is_missing
        case treat_nans_as_blank && self.sql_type.is_definitely_double of
            True -> is_blank || self.is_nan
            False -> is_blank

    ## UNSTABLE

       Returns a new column where missing values have been replaced with the
       provided default.
    fill_missing : Any -> Column
    fill_missing self default = self.make_binary_op "FILL_NULL" default

    ## UNSTABLE

       Returns the same column with changed name.

       Arguments:
       - new_name: The name to rename `self` column to.
    rename : Text -> Column ! Unsupported_Name
    rename self new_name =
        Column.Value new_name self.connection self.sql_type self.expression self.context

    ## UNSTABLE

       Sorts the column according to the specified rules.

       Arguments:
         - order: Specifies the default sort order for this operation.
         - missing_last: Specifies the default placement of missing values when
           compared to non-missing ones. Note that this argument is independent
           from `order`, i.e. missing values will always be sorted according to
           this rule, ignoring the ascending / descending setting.

       > Example
         Sorting `column` in ascending order.
             column.sort

       > Example
         Sorting `column` in descending order.
             column.sort Sort_Direction.Descending
    sort : Sort_Direction -> Column
    sort self order=Sort_Direction.Ascending =
        self.to_table.order_by (Sort_Column.Index 0 order) . at 0

    ## UNSTABLE
       Creates a new Column with the specified range of rows from the input
       Column.

       Arguments:
       - range: The selection of rows from the table to return.
    take : (Index_Sub_Range | Range | Integer) -> Column
    take self range=(First 1) =
        _ = range
        msg = "`Column.take` is not yet implemented."
        Error.throw (Unsupported_Database_Operation.Error msg)

    ## UNSTABLE
       Creates a new Column from the input with the specified range of rows
       removed.

       Arguments:
       - range: The selection of rows from the table to remove.
    drop : (Index_Sub_Range | Range | Integer) -> Column
    drop self range=(First 1) =
        _ = range
        msg = "`Column.drop` is not yet implemented."
        Error.throw (Unsupported_Database_Operation.Error msg)

    ## UNSTABLE

       Checks for each element of the column if it starts with `other`.

       Arguments:
       - other: A column or text to check for each item in `self`.
       - case_sensitivity: Specifies if the text values should be compared case
         sensitively.

       If `other` is a column, the operation is performed pairwise between
       corresponding elements of `self` and `other`.  If the argument is a
       missing value (a Nothing or a column with missing values), the behaviour
       on these missing values is vendor specific.
    starts_with : Column | Text -> Case_Sensitivity -> Column
    starts_with self other case_sensitivity=Case_Sensitivity.Default =
        make_text_case_op self "starts_with" other case_sensitivity

    ## UNSTABLE

       Checks for each element of the column if it ends with `other`.

       Arguments:
       - other: A column ot text to check for each item in `self`.
       - case_sensitivity: Specifies if the text values should be compared case
         sensitively.

       If `other` is a column, the operation is performed pairwise between
       corresponding elements of `self` and `other`.  If the argument is a
       missing value (a Nothing or a column with missing values), the behaviour
       on these missing values is vendor specific.
    ends_with : Column | Text -> Case_Sensitivity -> Column
    ends_with self other case_sensitivity=Case_Sensitivity.Default =
        make_text_case_op self "ends_with" other case_sensitivity

    ## UNSTABLE

       Checks for each element of the column if it contains `other`.

       Arguments:
       - other: A column ot text to check for each item in `self`.
       - case_sensitivity: Specifies if the text values should be compared case
         sensitively.

       If `other` is a column, the operation is performed pairwise between
       corresponding elements of `self` and `other`.  If the argument is a
       missing value (a Nothing or a column with missing values), the behaviour
       on these missing values is vendor specific.
    contains : Column | Text -> Case_Sensitivity -> Column
    contains self other case_sensitivity=Case_Sensitivity.Default =
        make_text_case_op self "contains" other case_sensitivity

    ## Checks for each element of the column if it matches an SQL-like pattern.

       Arguments:
       - pattern: The pattern to match `self` against. If it is a column, the
         operation is performed pairwise between corresponding elements of
         `self` and that column. The pattern is an SQL-like pattern, where
         `%` matches any sequence of characters and `_` matches any single
         character.

       > Example
         Check if elements of a column start with 'F' and end with a dot.

             import Standard.Examples

             example_contains = Examples.text_column_1.like "F%."
    like : Column | Text -> Column
    like self pattern = self.make_binary_op "LIKE" pattern new_type=SQL_Type.boolean

    ## Checks for each element of the column if it is contained within the
       provided vector or column.

       Arguments:
       - vector: A vector of elements or another column. The resulting column
         will contain true at the positions where the corresponding element of
         `self` is contained in `vector`.

       > Example
         Check if elements of a column are contained in a provided vector.

             import Standard.Examples

             example_contains = Examples.text_column_1.is_in [1, 2, 5]
    is_in : Column | Vector -> Column
    is_in self vector = case vector of
        _ : Vector ->
            ## This is slightly hacky - we don't provide operand types as we want to
               allow any type to get through and currently we do not have a mapping
               from Enso types to SQL types (it may be available in the future). So
               we just rely on Nothing resolving to the current column type. That
               type may not always match the operands, but the current
               implementation uses this type only for two purposes: generated SQL
               visualization (so the color will be consistent with the column type
               and not the value type - that can be confusing, we probably want to
               fix it later) and setting up the query - but at the set up this only
               applies to adding nulls - setting any other object does not check the
               type at this level anyway.
            partitioned = vector.partition .is_nothing
            nulls = partitioned.first
            non_nulls = partitioned.second
            ## Since SQL `NULL IN (NULL)` yields `NULL`, we need to handle this case
               separately. So we handle all non-null values using `IS_IN` and then
               `OR` that with a null check (if the vector contained any nulls to
               begin with). The implementation also ensures that even
               `NULL IN (...)` is coalesced to False, so that negation works as
               expected.
            is_in_not_null = self.make_op "IS_IN" operands=non_nulls new_type=SQL_Type.boolean
            case nulls.not_empty of
                True -> is_in_not_null || self.is_missing
                False -> is_in_not_null
        _ : Array -> self.is_in (Vector.from_polyglot_array vector)
        column : Column -> if Helpers.check_connection self column . not then (Error.throw (Integrity_Error.Error "Column "+column.name)) else
            ## We slightly abuse the expression syntax putting a Query as one of
               the sub-expressions. Once type-checking is added, we may need to
               amend the signature of `SQL_Expression.Operation` to account for
               this. Also, unfortunately as `NULL IN (...)` is `NULL` in SQL, we
               need to do separate handling of nulls - we check if the target
               column has any nulls and if so, we will do `IS NULL` checks for
               our columns too. That is because, we want the containment check
               for `NULL` to work the same way as for any other value.
            in_subquery = Query.Select [Pair.new column.name column.expression] column.context
            has_nulls_expression = SQL_Expression.Operation "BOOL_OR" [column.is_missing.expression]
            has_nulls_subquery = Query.Select [Pair.new "has_nulls" has_nulls_expression] column.context
            new_expr = SQL_Expression.Operation "IS_IN_COLUMN" [self.expression, in_subquery, has_nulls_subquery]
            Column.Value self.name self.connection SQL_Type.boolean new_expr self.context

    ## Parsing values is not supported in database columns.
    @type Widget_Helpers.parse_type_selector
    parse : (Auto|Integer|Decimal|Date|Date_Time|Time_Of_Day|Boolean) -> Text | Data_Formatter -> Problem_Behavior -> Column
    parse self type=Auto format=Data_Formatter.Value on_problems=Report_Warning =
        _ = [type, format, on_problems]
        Error.throw <| Unsupported_Database_Operation.Error "`Column.parse` is not implemented yet for the Database backends."

    ## PRIVATE
    as_internal : Internal_Column
    as_internal self = Internal_Column.Value self.name self.sql_type self.expression

    ## Provides a simplified text representation for display in the REPL and errors.
    to_text : Text
    to_text self = "(Database Column "+self.name.to_text+")"

    ## PRIVATE
       Helper for the expression to tell it which functions needs a Vector.
    var_args_functions : Array
    var_args_functions = ['is_in', 'coalesce', 'min', 'max']

## PRIVATE
   TODO: Revisit this as part of the column value type work.
get_approximate_type value default = case value of
    _ : Column -> value.sql_type
    Nothing -> default
    _ -> SQL_Type.approximate_type value

## PRIVATE
   Helper for case case_sensitivity based text operations
make_text_case_op left op other case_sensitivity =
    Value_Type.expect_text left.value_type <| case case_sensitivity of
        Case_Sensitivity.Default -> left.make_binary_op op other new_type=SQL_Type.boolean
        Case_Sensitivity.Sensitive ->
            cs_other = if other.is_a Column then other.make_unary_op "MAKE_CASE_SENSITIVE" else other
            left.make_unary_op "MAKE_CASE_SENSITIVE" . make_binary_op op cs_other new_type=SQL_Type.boolean
        Case_Sensitivity.Insensitive locale -> Helpers.assume_default_locale locale <|
            ci_other = if other.is_a Column then other.make_unary_op "FOLD_CASE" else other.to_case Case.Lower
            left.make_unary_op "FOLD_CASE" . make_binary_op op ci_other new_type=SQL_Type.boolean

## PRIVATE
make_equality_check_with_floating_point_handling column other op =
    other_name = case other of
        other_column : Column -> "["+other_column.name+"]"
        _ -> other.to_text
    location = "["+column.name+"] "+op+" "+other_name
    problem_builder = MapOperationProblemBuilder.new location
    if column.value_type.is_floating_point then
        problem_builder.reportFloatingPointEquality -1
    case other of
        other_column : Column ->
            if other_column.value_type.is_floating_point then
                problem_builder.reportFloatingPointEquality -1
        _ : Decimal ->
            problem_builder.reportFloatingPointEquality -1
        _ -> Nothing
    result = column.make_binary_op op other new_type=SQL_Type.boolean
    Problem_Behavior.Report_Warning.attach_problems_after result <|
        Java_Problems.parse_aggregated_problems problem_builder.getProblems
