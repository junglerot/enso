from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.System.File_Format.File_For_Read

import project.Connection.Database
import project.Connection.SQLite_Details.SQLite_Details

## Read the file to a `SQLite_Connection` from a `.db` or `.sqlite` file
type SQLite_Format
    ## Read SQLite files
    For_File

    ## PRIVATE
       If the File_Format supports reading from the file, return a configured instance.
    for_file_read : File_For_Read -> SQLite_Format | Nothing
    for_file_read file:File_For_Read =
        expected_header = magic_header_string
        got_header = file.read_first_bytes expected_header.length
        if got_header == expected_header then SQLite_Format.For_File else
            ## To allow for reading a SQLite file by extension if we cannot read the file header.
            if got_header.is_nothing.not then Nothing else case file.extension of
                ".db" -> SQLite_Format.For_File
                ".sqlite" -> SQLite_Format.For_File
                _ -> Nothing

    ## PRIVATE
       If the File_Format supports writing to the file, return a configured instance.
    for_file_write : File -> SQLite_Format | Nothing
    for_file_write file =
        case file.extension of
            ".db" -> SQLite_Format.For_File
            ".sqlite" -> SQLite_Format.For_File
            _ -> Nothing

    ## PRIVATE
       If the File_Format supports reading from the web response, return a configured instance.
    for_web : Text -> URI -> SQLite_Format | Nothing
    for_web content_type uri =
        ## Currently not loading SQLite files automatically.
        _ = [content_type, uri]
        Nothing

    ## PRIVATE
       Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any
    read self file on_problems =
        _ = [on_problems]
        Database.connect (SQLite_Details.SQLite file)

## PRIVATE
   Based on the File Format definition at: https://www.sqlite.org/fileformat.html
magic_header_string =
    "SQLite format 3".utf_8 + [0]
