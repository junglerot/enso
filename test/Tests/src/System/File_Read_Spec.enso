from Standard.Base import all
from Standard.Base.System.File_Format import Unsupported_File_Type_Data

from Standard.Test import Test, Test_Suite, Problems

spec =
    sample_xxx = enso_project.data / "sample.xxx"
    sample_txt = enso_project.data / "helloworld.txt"
    windows_log = enso_project.data / "windows.log"

    Test.group "Auto_Detect" <|
        Test.specify "should raise an error when reading an unknown file" <|
            bytes = sample_xxx.read
            bytes.should_fail_with Unsupported_File_Type_Data

        Test.specify "should be able to read a text file" <|
            content = sample_txt.read
            content.should_equal "Hello World!"

    Test.group "Bytes" <|
        Test.specify "should be able to read a file as Bytes" <|
            bytes = sample_xxx.read Bytes
            bytes.should_equal [72, 101, 108, 108, 111, 32, 87, 111, 114, 108, 100, 33]

        Test.specify "should be able to read a file as Bytes by method" <|
            bytes = File.read (sample_xxx) Bytes
            bytes.should_equal [72, 101, 108, 108, 111, 32, 87, 111, 114, 108, 100, 33]

        Test.specify "should be able to read a file as Bytes by path" <|
            path = sample_xxx.path
            bytes = File.read path Bytes
            bytes.should_equal [72, 101, 108, 108, 111, 32, 87, 111, 114, 108, 100, 33]

    Test.group "Plain_Text" <|
        Test.specify "should be able to read a file as Text" <|
            text = sample_xxx.read Plain_Text
            text.should_equal "Hello World!"

        Test.specify "should be able to read a file as Text with Encoding" <|
            text = windows_log.read (Plain_Text Encoding.windows_1252)
            text.should_equal "Hello World! $¢¤¥"

        Test.specify "should raise a warning when invalid encoding in a Text file" <|
            action = windows_log.read (Plain_Text Encoding.ascii) on_problems=_
            tester result = result . should_equal 'Hello World! $\uFFFD\uFFFD\uFFFD'
            problems = [Encoding_Error_Data "Encoding issues at 14, 15, 16."]
            Problems.test_problem_handling action problems tester

main = Test_Suite.run_main spec
