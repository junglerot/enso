from Base import all

import Base.Test
import Base.Net.Http.Form
import Base.Net.Http.Header
import Base.Net.Http.Method
import Base.Net.Http.Request
import Base.Net.Http.Request.Body as Request_Body
import Base.Net.Uri

spec =
    test_uri = Uri.parse "https://httpbin.org/post"
    test_headers = [Header.application_json, Header.new "X-Foo-Id" "0123456789"]
    describe "Request" <|
        it "should get method" <|
            req = Request.new Method.Post test_uri
            req.method.should_equal Method.Post
        it "should get uri" <|
            req = Request.get test_uri
            req.uri.should_equal test_uri
        it "should get headers" <|
            req = Request.get test_uri test_headers
            req.headers.should_equal test_headers
        it "should add header" <|
            new_header = Header.accept_all
            req = Request.get test_uri test_headers . with_header new_header.name new_header.value
            req.headers.should_equal (test_headers + [new_header])
        it "should update header" <|
            req = Request.get test_uri test_headers . with_header "X-Foo-Id" "42"
            req.headers.should_equal [Header.application_json, Header.new "X-Foo-Id" "42"]
        it "should add headers" <|
            req = Request.get test_uri . with_headers test_headers
            req.headers.should_equal test_headers
        it "should update headers" <|
            new_headers = [Header.multipart_form_data, Header.accept_all]
            req = Request.get test_uri test_headers . with_headers new_headers
            req.headers.should_equal [Header.multipart_form_data, test_headers.at 1, Header.accept_all]
        it "should set json body" <|
            json = '''
                {"key":"val"}
            req = Request.get test_uri . with_json json
            req.body.should_equal (Request_Body.Json json)
            req.headers.should_equal [Header.application_json]
        it "should set form body" <|
            body_form = [Form.text_field "key" "val"]
            req = Request.get test_uri . with_form body_form
            req.body.should_equal (Request_Body.Form body_form.to_form)
            req.headers.should_equal [Header.application_x_www_form_urlencoded]
