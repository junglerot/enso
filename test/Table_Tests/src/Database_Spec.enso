from Standard.Base import all
from Standard.Database import all

import Standard.Test

import project.Common_Table_Spec

sqlite_spec =
    Enso_Project.data.create_directory
    file = Enso_Project.data / "sqlite_test.db"
    file.delete_if_exists
    connection = Database.open_sqlite_file file

    name_counter = Ref.new 0
    table_builder columns =
        ix = Ref.get name_counter
        Ref.put name_counter ix+1
        name = "table_"+ix.to_text
        quote x = '"' + x + '"'
        # TODO this is a hack with no sanitization, just for testing; it should be removed when proper create table is supported by the library
        column_definitions = columns.map col->
            name = col.first
            typ = case col.second of
                Integer -> "INT"
                _ -> Panic.throw "The provided type "+col.second+" is not currently supported by the test suite. It may need to be extended."
            quote name + " " + typ
        sql = "CREATE TABLE " + quote name + " (" + (column_definitions.join ", ") + ")"
        Panic.rethrow <| connection.execute_update sql
        table = Panic.rethrow <| connection.access_table name

        row_number = columns.first.at 2 . length
        0.up_to row_number . each ix->
            row = columns.map col-> col.at 2 . at ix
            table.insert row
        table

    Common_Table_Spec.spec "[SQLite] " table_builder supports_case_sensitive_columns=False

    connection.close
    file.delete

main = Test.Suite.run_main here.sqlite_spec
