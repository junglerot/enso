from Standard.Base import all
import Standard.Base.Error.Illegal_State.Illegal_State

from Standard.Table import Sort_Column
from Standard.Table.Data.Aggregate_Column.Aggregate_Column import all
from Standard.Table.Errors import No_Input_Columns_Selected, Missing_Input_Columns, No_Such_Column

from Standard.Database import all
import Standard.Database.Data.Dialect
import Standard.Database.Data.SQL_Type.SQL_Type
from Standard.Database.Data.Table import combine_names, fresh_names
from Standard.Database.Errors import Unsupported_Database_Operation

from Standard.Test import Test, Test_Suite, Problems
import Standard.Test.Extensions

import project.Database.Helpers.Fake_Test_Connection

spec =
    int = SQL_Type.integer
    bool = SQL_Type.boolean
    str = SQL_Type.text
    test_connection =
        table1 = ["T1", [["A", int], ["B", str], ["C", bool]]]
        table2 = ["T2", [["D", int], ["E", int], ["F", bool]]]
        table3 = ["T3", [["A", int], ["E", bool], ["F", int]]]
        tables = Map.from_vector [table1, table2, table3]
        Fake_Test_Connection.make Dialect.sqlite tables
    t1 = test_connection.query (SQL_Query.Table_Name "T1")
    Test.group "[Codegen] JSON serialization" <|
        Test.specify "should serialize Tables and Columns to their SQL representation" pending="TODO: decide on how we handle ==" <|
            q1 = t1.filter (t1.at "A" == 42) . to_json

            part1 = JS_Object.from_pairs [["sql_code", 'SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE ("T1"."A" = ']]
            part2_sub = JS_Object.from_pairs [["value", 42], ["expected_sql_type", "INTEGER"]]
            part2 = JS_Object.from_pairs [["sql_interpolation", part2_sub]]
            part3 = JS_Object.from_pairs [["sql_code", ")"]]
            expected = JS_Object.from_pairs [["query", [part1, part2, part3]]] . to_text
            q1.should_equal expected

            q2 = t1.at "A" . to_json
            expected_2 = JS_Object.from_pairs [["query", [JS_Object.from_pairs [["sql_code", 'SELECT "T1"."A" AS "A" FROM "T1" AS "T1"']]]]] . to_text
            q2.should_equal expected_2

    Test.group "[Codegen] Basic Select" <|
        Test.specify "should select columns from a table" <|
            t1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1"', []]
            t2 = t1.select_columns ["C", "B", "undefined"] reorder=True error_on_missing_columns=False
            t2.to_sql.prepare . should_equal ['SELECT "T1"."C" AS "C", "T1"."B" AS "B" FROM "T1" AS "T1"', []]

            foo = t1.at "A" . rename "FOO"
            foo.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "FOO" FROM "T1" AS "T1"', []]

            t3 = t2.set foo new_name="bar"
            t3.to_sql.prepare . should_equal ['SELECT "T1"."C" AS "C", "T1"."B" AS "B", "T1"."A" AS "bar" FROM "T1" AS "T1"', []]

        Test.specify "should fail if at is called for a non-existent column" <|
            t1.at "undefined" . should_fail_with No_Such_Column

        Test.specify "should allow to limit the amount of returned results" <|
            t2 = t1.limit 5
            t2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" LIMIT 5', []]

    Test.group "[Codegen] Building Expressions" <|
        Test.specify "should allow building expressions from columns and constants" <|
            t2 = test_connection.query (SQL_Query.Table_Name "T2")
            d = t2.at "D"
            e = t2.at "E"
            f = t2.at "F"
            arith = (d * 2) + 1
            logic = (f || f.not) && True
            cmp = (d * d >= e) && (d - e < d)
            arith.to_sql.prepare . should_equal ['SELECT (("T2"."D" * ?) + ?) AS "'+arith.name+'" FROM "T2" AS "T2"', [[2, int], [1, int]]]
            logic.to_sql.prepare . should_equal ['SELECT (("T2"."F" OR (NOT "T2"."F")) AND ?) AS "'+logic.name+'" FROM "T2" AS "T2"', [[True, bool]]]
            cmp.to_sql.prepare . should_equal ['SELECT ((("T2"."D" * "T2"."D") >= "T2"."E") AND (("T2"."D" - "T2"."E") < "T2"."D")) AS "'+cmp.name+'" FROM "T2" AS "T2"', []]

        Test.specify "should support simple text operations" <|
            b = t1.at "B"
            add = b + "SUFFIX"
            add.to_sql.prepare . should_equal ['SELECT ("T1"."B" || ?) AS "[B] + \'SUFFIX\'" FROM "T1" AS "T1"', [["SUFFIX", str]]]

            ends = b.ends_with "suf"
            starts = b.starts_with "pref"
            contains = b.contains "inf"
            ends.to_sql.prepare . should_equal ['SELECT ("T1"."B" GLOB (\'*\' || ?)) AS "'+ends.name+'" FROM "T1" AS "T1"', [["suf", str]]]
            starts.to_sql.prepare . should_equal ['SELECT ("T1"."B" GLOB (? || \'*\')) AS "'+starts.name+'" FROM "T1" AS "T1"', [["pref", str]]]
            contains.to_sql.prepare . should_equal ['SELECT instr("T1"."B", ?) > 0 AS "'+contains.name+'" FROM "T1" AS "T1"', [["inf", str]]]

    Test.group "[Codegen] Masking Tables and Columns" <|
        Test.specify "should generate a single BETWEEN expression" <|
            t2 = t1.filter "A" (Filter_Condition.Between 10 20)
            t2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE ("T1"."A" BETWEEN ? AND ?)', [[10, int], [20, int]]]

            t3 = t1.filter "A" (Filter_Condition.Between (t1.at "B") (t1.at "C"))
            t3.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE ("T1"."A" BETWEEN "T1"."B" AND "T1"."C")', []]

            t4 = t1.filter "A" (Filter_Condition.Between (t1.at "B") 33)
            t4.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE ("T1"."A" BETWEEN "T1"."B" AND ?)', [[33, int]]]

        Test.specify "should generate an IN expression" <|
            t2 = t1.filter "A" (Filter_Condition.Is_In [1, 2, 'foo'])
            t2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE COALESCE("T1"."A" IN (?, ?, ?), FALSE)', [[1, int], [2, int], ["foo", int]]]

            t3 = t1.filter "A" (Filter_Condition.Is_In [1])
            t3.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE COALESCE("T1"."A" IN (?), FALSE)', [[1, int]]]

            t4 = t1.filter "A" (Filter_Condition.Is_In [])
            t4.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE (FALSE)', []]

            t5 = t1.filter "A" (Filter_Condition.Is_In [Nothing])
            t5.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE ((FALSE) OR ("T1"."A" IS NULL))', []]

    Test.group "[Codegen] Handling Missing Values" <|
        Test.specify "fill_nothing should allow to replace missing values in a column with a constant" <|
            c = t1.at "A" . fill_nothing "not-applicable"
            c.to_sql.prepare . should_equal ['SELECT COALESCE("T1"."A", ?) AS "fill_nothing([A], \'not-applicable\')" FROM "T1" AS "T1"', [["not-applicable", int]]]

        Test.specify "filter_blank_rows should drop rows that contain at least one missing column in a Table" <|
            t2 = t1.filter_blank_rows when_any=True
            t2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE (NOT ((("T1"."A" IS NULL) OR (("T1"."B" IS NULL) OR ("T1"."B" = \'\'))) OR ("T1"."C" IS NULL)))', []]

            t3 = t1.filter_blank_rows when_any=False
            t3.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE (NOT ((("T1"."A" IS NULL) AND (("T1"."B" IS NULL) OR ("T1"."B" = \'\'))) AND ("T1"."C" IS NULL)))', []]

    Test.group "[Codegen] Sorting" <|
        Test.specify "should allow sorting by a single column name" <|
            r1 = t1.order_by ([Sort_Column.Name "A"]) . at "B"
            r1.to_sql.prepare . should_equal ['SELECT "T1"."B" AS "B" FROM "T1" AS "T1" ORDER BY "T1"."A" ASC', []]

            r2 = t1.order_by ([Sort_Column.Name "B" Sort_Direction.Descending]) . at "A"
            r2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A" FROM "T1" AS "T1" ORDER BY "T1"."B" DESC', []]

        Test.specify 'should allow sorting by multiple column names' <|
            r1 = t1.order_by ([Sort_Column.Name 'A', Sort_Column.Name 'B'])
            r1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" ORDER BY "T1"."A" ASC, "T1"."B" ASC', []]

        Test.specify 'should allow sorting with specific by-column rules' <|
            r1 = t1.order_by ([Sort_Column.Name "A", Sort_Column.Name "B" Sort_Direction.Descending])
            r1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" ORDER BY "T1"."A" ASC, "T1"."B" DESC', []]

        Test.specify 'should return warnings and errors when passed a non-existent column' <|
            t2 = t1.order_by ([Sort_Column.Name 'foobar'])
            t2.should_fail_with Missing_Input_Columns

    Test.group "Helpers" <|
        Test.specify "fresh_names should provide fresh names" <|
            used_names = ["A", "A_1"]
            preferred_names = ["A", "A", "B"]
            fresh_names used_names preferred_names . should_equal ["A_2", "A_3", "B"]

    Test.group "[Codegen] Aggregation" <|
        Test.specify "should allow to count rows" <|
            code = t1.aggregate [Group_By "A" "A grp", Count "counter"] . to_sql . prepare
            code . should_equal ['SELECT "T1"."A grp" AS "A grp", "T1"."counter" AS "counter" FROM (SELECT "T1"."A" AS "A grp", COUNT(*) AS "counter" FROM "T1" AS "T1" GROUP BY "T1"."A") AS "T1"', []]

        Test.specify "should allow to group by multiple fields" <|
            code = t1.aggregate [Sum "A" "sum_a", Group_By "C" Nothing, Group_By "B" "B grp"] . to_sql . prepare
            code . should_equal ['SELECT "T1"."sum_a" AS "sum_a", "T1"."C" AS "C", "T1"."B grp" AS "B grp" FROM (SELECT SUM("T1"."A") AS "sum_a", "T1"."C" AS "C", "T1"."B" AS "B grp" FROM "T1" AS "T1" GROUP BY "T1"."C", "T1"."B") AS "T1"', []]

main = Test_Suite.run_main spec
