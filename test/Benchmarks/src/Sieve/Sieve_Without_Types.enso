from Standard.Base import Any, Meta, Integer, True, False, Nothing

type Gen
    Generator n

    next self = Gen.Generator self.n+1

natural =
    gen n = Gen.Generator n
    gen 2

type Filter
    Empty
    Item n next
    Head first set_last

    acceptAndAdd self n =
        iterate f = case f of
            Filter.Item p next -> if n % p == 0 then False else
                if p * p > n then True else @Tail_Call iterate next
            Filter.Empty -> True

        appendN head set_last =
            pair = Meta.atom_with_hole (h -> Filter.Item n h)
            newLast = pair.value
            set_last newLast
            Filter.Head head.first pair.fill

        case self of
            Filter.Empty ->
                pair = Meta.atom_with_hole (h -> Filter.Item n h)
                Filter.Head pair.value pair.fill
            Filter.Head _ _ -> if iterate self.first then appendN self self.set_last else self

type Primes
    Alg generator filter

Primes.next self = case self of
    Primes.Alg g f ->
        filter = f.acceptAndAdd g.n
        nf = if filter.is_nothing then f else filter
        new_primes = Primes.Alg g.next nf
        if filter.is_nothing || Meta.is_same_object filter f then @Tail_Call new_primes.next else new_primes

Primes.last_prime self = case self of
    Primes.Alg g _ -> g.n - 1


compute_nth_prime n gen=natural fil=Filter.Empty =
    loop p n = if n == 0 then p else
        pn = p.next
        @Tail_Call loop pn n-1
    p = Primes.Alg gen fil
    loop p n . last_prime
