from Standard.Base import all
from Standard.Base.Data.Array_Proxy import Array_Proxy
import Standard.Base.Errors.Common.Index_Out_Of_Bounds
import Standard.Base.Errors.Common.Type_Error
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

from Standard.Test_New import all


type Proxy_Object
    Value length

    at : Integer -> Integer
    at self ix = ix * 10

add_specs suite_builder =
    suite_builder.group "Array_Proxy" group_builder->
        group_builder.specify "should correctly delegate to the callback" <|
            arr = Array_Proxy.new 3 (ix -> ix + 10)
            arr.length . should_equal 3
            arr.at 0 . should_equal 10
            arr.at 1 . should_equal 11
            arr.at 2 . should_equal 12
            arr.at 3 . should_fail_with Index_Out_Of_Bounds

        group_builder.specify "should be able to be used to construct a Vector" <|
            v1 = Vector.from_polyglot_array (Array_Proxy.new 3 (ix -> ix + 10))
            v1.length . should_equal 3
            v1 . should_equal [10, 11, 12]

            v1.map (x -> x + 1) . should_equal [11, 12, 13]

            v2 = Vector.from_polyglot_array (Array_Proxy.new 3 (ix -> v1.at 2 - ix))
            v2.should_equal [12, 11, 10]
            v2.sort . should_equal [10, 11, 12]

        group_builder.specify "should be able to construct a Vector from a proxy object" <|
            v1 = Vector.from_polyglot_array (Array_Proxy.from_proxy_object [4, 3, 2])
            v1.should_equal [4, 3, 2]

            v2 = Vector.from_polyglot_array (Array_Proxy.from_proxy_object (Proxy_Object.Value 5))
            v2.should_equal [0, 10, 20, 30, 40]

        group_builder.specify "should check validity at construction" <|
            Test.expect_panic_with (Array_Proxy.new 0 0) Type_Error
            Test.expect_panic_with (Array_Proxy.new -1 (x->x)) Illegal_Argument

        group_builder.specify "should pretend to just be an Array" <|
            proxy = Array_Proxy.new 3 (ix -> ix + 10)
            ok_match = case proxy of
                _ : Vector -> "Vector"
                _ : Array -> "Array"
                _ -> "other"
            ok_match . should_equal "Array"
            Meta.get_qualified_type_name proxy . should_equal "Standard.Base.Data.Array.Array"
            Meta.get_simple_type_name proxy . should_equal "Array"

main =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter

