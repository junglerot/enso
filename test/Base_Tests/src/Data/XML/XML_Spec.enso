from Standard.Base import all
import Standard.Base.Errors.Common.Syntax_Error
import Standard.Base.Errors.File_Error.File_Error
from Standard.Base.Runtime import assert

from Standard.Test_New import all

type Test_Data
    Value ~data
    
    test_file self = self.data.at 0
    document self = self.data.at 1
    root self = self.data.at 2

    setup = Test_Data.Value <|
        test_file = enso_project.data / "xml" / "sample.xml"
        assert test_file.exists
        document = XML_Document.from_file test_file
        root = document . root_element
        [test_file, document, root]


add_specs suite_builder =
    data = Test_Data.setup

    suite_builder.group "Read XML" group_builder->
        group_builder.specify "Can read from a file" <|
            data.root.name . should_equal "class"

        group_builder.specify "Error if file does not exist" <|
            test_file = enso_project.data / "xml" / "sample.xmlnotexists"
            XML_Document.from_file test_file . should_fail_with File_Error

        group_builder.specify "Can read from a stream" <|
            data.test_file.with_input_stream [File_Access.Read] input_stream->
                doc = XML_Document.from_stream input_stream
                doc.root_element.name . should_equal "class"

        group_builder.specify "Can read from a string" <|
            xml_string = data.test_file.read_text
            doc = XML_Document.from_text xml_string
            doc.root_element.name . should_equal "class"

        group_builder.specify "Can read from a short string" <|
            xml_string = "<class></class>"
            doc = XML_Document.from_text xml_string
            doc.root_element.name . should_equal "class"

        group_builder.specify "Parse error from file" <|
            test_file = enso_project.data / "sample.txt"
            XML_Document.from_file test_file . catch . should_be_a XML_Error.Parse_Error

        group_builder.specify "Parse error from string" <|
            xml_string = "<<<<</"
            XML_Document.from_text xml_string . catch . should_be_a XML_Error.Parse_Error

    suite_builder.group "at/get" group_builder->
        group_builder.specify "Can get children by index" <|
            data.root.at 0 . name . should_equal "teacher"

            data.root.at 0 . at 0 . name . should_equal "firstname"
            data.root.at 0 . at 1 . name . should_equal "lastname"
            data.root.at 0 . at 2 . name . should_equal "bio"
            data.root.at 0 . at 2 . at 0 . should_equal '\n            Blah blah\n        '

            data.root.at 3 . at 0 . name . should_equal "firstname"
            data.root.at 3 . at 1 . name . should_equal "lastname"
            data.root.at 3 . at 2 . name . should_equal "gpa"
            data.root.at 3 . at 2 . at 0 . should_equal "3.99"

        group_builder.specify "Can get text children by index" <|
            data.root.at 4 . at 0 . should_equal '\n        Some\n        '
            data.root.at 4 . at 2 . should_equal '\n        Extra\n        '
            data.root.at 4 . at 4 . should_equal '\n        Text\n        '

        group_builder.specify "Can get element attributes" <|
            data.root.at 0 . at "@id" . should_equal "100"
            data.root.at 1 . at "@id" . should_equal "101"
            data.root.at 2 . at "@studentId" . should_equal "1000"
            data.root.at 3 . at "@studentId" . should_equal "1001"

            data.root.at 0 . attribute "id" . should_equal "100"
            data.root.at 1 . attribute "id" . should_equal "101"
            data.root.at 2 . attribute "studentId" . should_equal "1000"
            data.root.at 3 . attribute "studentId" . should_equal "1001"

            data.root.at 3 . attribute "does_not_exist" if_missing="if_missing" . should_equal "if_missing"

        group_builder.specify "Can get element an attribute map" <|
            data.root.at 2 . attributes . should_equal (Map.from_vector [["studentId", "1000"], ["year", "2"]])
            data.root.at 3 . attributes . should_equal (Map.from_vector [["studentId", "1001"], ["year", "3"]])

        group_builder.specify "Can get nodes via xpath" <|
            classes = data.root.at "/class"
            classes.length . should_equal 1
            classes.at 0 . name . should_equal "class"

            teachers = data.root.at "/class/teacher"
            teachers.length . should_equal 2
            teachers.at 0 . at "@id" . should_equal "100"
            teachers.at 1 . at "@id" . should_equal "101"

            students = data.root.at "/class/student"
            students.length . should_equal 3
            students.at 0 . at "@studentId" . should_equal "1000"
            students.at 1 . at "@studentId" . should_equal "1001"

            data.root.at "/class/teacher[1]/firstname" . at 0 . text . should_equal "Mary"
            data.root.at "/class/teacher[2]/firstname" . at 0 . text . should_equal "Bob"
            data.root.at "/class/teacher[1]/firstname/text()" . should_equal ["Mary"]
            data.root.at "/class/teacher[2]/firstname/text()" . should_equal ["Bob"]
            data.root.at "/class/teacher/firstname/text()" . should_equal ["Mary", "Bob"]
            data.root.at "/class/teacher[1]/bio" . at 0 . text . should_equal '\n            Blah blah\n        '
            data.root.at "/class/teacher[2]/bio" . at 0 . text . should_equal '\n            This that\n        '
            data.root.get "/class/teacher[23]" . should_equal []

            data.root.at "teacher[1]/firstname" . at 0 . text . should_equal "Mary"
            data.root.at "teacher[2]/firstname" . at 0 . text . should_equal "Bob"
            data.root.at "teacher[1]/bio" . at 0 . text . should_equal '\n            Blah blah\n        '
            data.root.at "teacher[2]/bio" . at 0 . text . should_equal '\n            This that\n        '

        group_builder.specify "Can get children using .get" <|
            data.root.get 0 . get 0 . name . should_equal "firstname"
            data.root.get 0 . get "@id" . should_equal "100"
            data.root.get "/class/teacher[1]/firstname" . get 0 . text . should_equal "Mary"

            data.root.get 0 . get 32 "if_missing" . should_equal "if_missing"
            data.root.get 0 . get "@not_there" "if_missing" . should_equal "if_missing"

        group_builder.specify "Can handle a bad xpath" <|
            data.root.at "/qqq[[[[1" . at 0 . text . should_fail_with XML_Error

    suite_builder.group "tag name" group_builder->
        group_builder.specify "Can get the tag name" <|
            data.root.name . should_equal "class"
            data.root.at 0 . name . should_equal "teacher"
            data.root.at 1 . at 1 . name . should_equal "lastname"

    suite_builder.group "children" group_builder->
        group_builder.specify "Can get the list of children" <|
            children = data.root.children
            children.length . should_equal 5
            children.at 0 . at "@id" . should_equal "100"
            children.at 1 . at "@id" . should_equal "101"
            children.at 2 . at "@studentId" . should_equal "1000"
            children.at 3 . at "@studentId" . should_equal "1001"
            children.at 4 . at "@studentId" . should_equal "1002"

        group_builder.specify "Can get the number of children" <|
            data.root.child_count . should_equal 5

    suite_builder.group "text contents" group_builder->
        group_builder.specify "Can get child text contents" <|
            data.root.at 4 . at 1 . text . should_equal "Randy"
            data.root.at 4 . text . should_equal '\n        Some\n        Randy\n        Extra\n        Brown\n        Text\n        3.99\n    '

    suite_builder.group "inner / outer xml" group_builder->
        group_builder.specify "Can get the inner xml" <|
            (data.root.at "/class/teacher[1]" . at 0 . inner_xml) . should_equal '\n        <firstname>Mary</firstname>\n        <lastname>Smith</lastname>\n        <bio>\n            Blah blah\n        </bio>\n    '
            (data.root.at "/class/teacher[1]/bio" . at 0 . inner_xml) . should_equal '\n            Blah blah\n        '
            (data.root.at "/class/teacher[2]/bio" . at 0 . inner_xml) . should_equal '\n            This that\n        '
            (data.root.at "/class/teacher[2]" . at 0 . inner_xml) . should_equal '\n        <firstname>Bob</firstname>\n        <lastname>Jones</lastname>\n        <bio>\n            This that\n        </bio>\n    '

        group_builder.specify "Can get the outer xml" <|
            (data.root.at "/class/teacher[1]/bio" . at 0 . outer_xml) . should_equal '<bio>\n            Blah blah\n        </bio>'
            (data.root.at "/class/teacher[2]/bio" . at 0 . outer_xml) . should_equal '<bio>\n            This that\n        </bio>'

    suite_builder.group "get_elements_by_tag_name" group_builder->
        group_builder.specify "Can get elements by tag name" <|
            teachers = data.root.get_elements_by_tag_name "teacher"
            students = data.root.get_elements_by_tag_name "student"
            gpas = data.root.get_elements_by_tag_name "gpa"

            teachers.length . should_equal 2
            students.length . should_equal 3
            gpas.length . should_equal 3

            teachers.at 0 . at "@id" . should_equal "100"
            teachers.at 1 . at "@id" . should_equal "101"
            students.at 0 . at "@studentId" . should_equal "1000"
            students.at 1 . at "@studentId" . should_equal "1001"
            students.at 2 . at "@studentId" . should_equal "1002"
            gpas.at 0 . text . should_equal "4.01"
            gpas.at 1 . text . should_equal "3.99"
            gpas.at 2 . text . should_equal "3.99"

        group_builder.specify "Can get nested elements" <|
            test_file = enso_project.data / "xml" / "nested.xml"
            root = XML_Document.from_file test_file . root_element
            bars = root.get_elements_by_tag_name "bar"
            bars.length . should_equal 4
            bars.map (t-> t.at "@id") . should_equal ["2", "4", "5", "6"]

        group_builder.specify "Can get elements by name with a wildcard" <|
            data.root.get_elements_by_tag_name "*" . length . should_equal 20

    suite_builder.group "Data.read / File_Format" group_builder->
        group_builder.specify "Can read from a file" <|
            doc = Data.read data.test_file
            doc.root_element.name . should_equal "class"

        group_builder.specify "Can read from an endpoint" <|
            doc = Data.fetch "https://enso-data-samples.s3.us-west-1.amazonaws.com/sample.xml"
            doc.root_element.name . should_equal "class"
            doc.root_element.at 1 . name . should_equal "teacher"

    suite_builder.group "display text" group_builder->
        group_builder.specify "Can generate display text" <|
            data.document.to_display_text . should_equal 'XML_Document (XML_Element "class")'
            data.root.to_display_text .  should_equal 'XML_Element "class"'

    suite_builder.group "JSON" group_builder->
        group_builder.specify "Can convert to JS_Object" <|
            test_file = enso_project.data / "xml" / "small.xml"
            document = XML_Document.from_file test_file
            root = document . root_element
            expected = Json.parse '''
                {
                "type": "XML_Element",
                "tag": "class",
                "attributes": [],
                "children": [
                    {
                    "type": "XML_Element",
                    "tag": "teacher",
                    "attributes": [ [ "id", "100" ]
                    ],
                    "children": [
                        { "type": "XML_Element", "tag": "firstname", "attributes": [], "children": [ "Mary" ] },
                        { "type": "XML_Element", "tag": "lastname", "attributes": [], "children": [ "Smith" ] },
                        { "type": "XML_Element", "tag": "bio", "attributes": [], "children": [ "\\n            Blah blah\\n        " ] }
                    ]
                    }
                ]
                }
            js = root.to_js_object
            js.should_equal expected

main =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter

