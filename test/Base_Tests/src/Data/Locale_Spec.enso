from Standard.Base import all

from Standard.Base.Metadata.Choice import Option
from Standard.Test_New import all


polyglot java import java.util.Locale as JavaLocale

with_locale locale ~test =
    default_locale = JavaLocale.getDefault
    JavaLocale.setDefault locale.java_locale
    result = Panic.catch Any test caught_panic->
        JavaLocale.setDefault default_locale
        Panic.throw caught_panic
    JavaLocale.setDefault default_locale
    result

add_specs suite_builder =
  suite_builder.group "Locale" group_builder->
    en_gb = Locale.new "en" "GB"
    group_builder.specify "allow constructing a locale with optional parts" <|
        loc = Locale.new "en"
        loc.language . should_equal "en"
        loc.country . should_equal Nothing
        loc.variant . should_equal Nothing
    group_builder.specify "should have a well-defined text conversion" <|
        en_gb.to_text . should_equal "en-GB"
    group_builder.specify "should allow obtaining it from a language tag" <|
        locale = Locale.from_language_tag "en-US"
        locale.to_text . should_equal "en-US"
    group_builder.specify "should allow getting the language code" <|
        locale = Locale.from_language_tag "en-US"
        locale.language . should_equal "en"
    group_builder.specify "should allow getting the country code" <|
        locale = Locale.from_language_tag "en-US"
        locale.country . should_equal "US"
    group_builder.specify "should allow getting the variant code" <|
        locale = Locale.from_language_tag "en-US-x-lvariant-UTF-8"
        locale.variant . should_equal "UTF_8"
    group_builder.specify "should allow getting the display language" <|
        with_locale Locale.us <|
            display = Locale.from_language_tag "en-GB" . display_language
            display . should_equal "English"
    group_builder.specify "should allow getting the display country" <|
        with_locale Locale.us <|
            display = Locale.from_language_tag "en-GB" . display_country
            display . should_equal "United Kingdom"
    group_builder.specify "should allow getting the display variant" <|
        display = Locale.from_language_tag "en-GB-x-lvariant-UTF8" . display_variant
        display . should_equal "UTF8"
    group_builder.specify "should have some common locale constants" <|
        Locale.default.to_text . should_equal "und"
        Locale.bangladesh.to_text . should_equal "bn-BD"
        Locale.brazil.to_text . should_equal "pt-BR"
        Locale.canada_english.to_text . should_equal "en-CA"
        Locale.canada_french.to_text . should_equal "fr-CA"
        Locale.china.to_text . should_equal "zh-CN"
        Locale.france.to_text . should_equal "fr-FR"
        Locale.germany.to_text . should_equal "de-DE"
        Locale.india_hindi.to_text . should_equal "hi-IN"
        Locale.india_english.to_text . should_equal "en-IN"
        Locale.indonesia.to_text . should_equal "id-ID"
        Locale.italy.to_text . should_equal "it-IT"
        Locale.japan.to_text . should_equal "jp-JP"
        Locale.mexico.to_text . should_equal "es-MX"
        Locale.nigeria.to_text . should_equal "en-NG"
        Locale.pakistan_urdu.to_text . should_equal "ur-PK"
        Locale.pakistan_english.to_text . should_equal "en-PK"
        Locale.russia.to_text . should_equal "ru-RU"
        Locale.south_korea.to_text . should_equal "ko-KR"
        Locale.uk.to_text . should_equal "en-GB"
        Locale.us.to_text . should_equal "en-US"
    group_builder.specify "should convert to Json" <|
        en_gb.to_json.should_equal '{"type":"Locale","constructor":"new","language":"en","country":"GB","variant":null}'
    group_builder.specify "should allow equality comparisons" <|
        Locale.uk . should_equal Locale.uk
        Locale.uk . should_not_equal Locale.us

    group_builder.specify "Should correctly auto-discover static Locales" <|
        locale_names = Locale.widget_options.map x-> case x of Option locale_name _ _ _ -> locale_name
        locale_names . should_equal ['Bangladesh', 'Brazil', 'Canada English', 'Canada French', 'China', 'France', 'Germany', 'India English', 'India Hindi', 'Indonesia', 'Italy', 'Japan', 'Mexico', 'Nigeria', 'Pakistan English', 'Pakistan Urdu', 'Poland', 'Russia', 'South Korea', 'UK', 'US']

main =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter

