from Standard.Base import all

from Standard.Test_New import all


Boolean.method self = self

type My_Error
    Value a

crash =
  Error.throw (My_Error.Value "foo")

add_specs suite_builder =
    suite_builder.group "Booleans" group_builder->
        group_builder.specify "should allow converting Bools to Text values" <|
            True.to_text . should_equal "True"
            False.to_text . should_equal "False"

        group_builder.specify "should allow for comparing Bools" <|
            (True == True) . should_be_true
            (False == False) . should_be_true
            (True > False) . should_be_true
            (False < True) . should_be_true

        group_builder.specify "should allow == operator" <|
            True.should_equal True
            False.should_equal False
            True.should_not_equal False
            False.should_not_equal True
            (1 == 1).should_equal True

        group_builder.specify "should allow for extending Bools in a local module" <|
            test = 1 == 2
            test.method . should_equal test

        group_builder.specify "should short-circuit ||" <|
            (1 == 1) || (crash) . should_equal True
            (1 == 0) || (1 == 1) . should_equal True
            (1 == 0) || (crash) . should_fail_with My_Error
            (1 == 1) || "foo" . should_equal True
            (1 == 0) || "foo" . should_equal "foo"

        group_builder.specify "should short-circuit &&" <|
            (1 == 0) && (crash) . should_equal False
            (1 == 1) && (1 == 0) . should_equal False
            (1 == 1) && (1 == 1) . should_equal True
            (1 == 1) && (crash) . should_fail_with My_Error
            (1 == 0) && "foo" . should_equal False
            (1 == 1) && "foo" . should_equal "foo"

main =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter

