import Standard.Base.Runtime
import Standard.Base.Data.Numbers.Integer
import Standard.Base.Any.Any
import Standard.Base.Panic.Panic

from Standard.Base.Runtime.Context import Input, Output

from Standard.Test_New import all


in_fn : Integer -> Integer
in_fn a = Input.if_enabled (a * 2)

out_fn : Integer -> Integer
out_fn a = Output.if_enabled (a + 1)

add_specs suite_builder =
    suite_builder.group "Inlining Helpers" group_builder->
        group_builder.specify "should allow to run an action" <|
            x = Runtime.no_inline (2 + 3)
            x . should_equal 5
        group_builder.specify "should allow to call a function" <|
            x = Runtime.no_inline_with_arg (x -> x + 4) 3
            x . should_equal 7
    suite_builder.group "Contexts and Execution Environment" group_builder->
        group_builder.specify "should not prevent execution in the default live environment" <|
            res = Panic.catch Any (in_fn 1) p-> p.payload.to_text
            res . should_equal 2
        group_builder.specify "should prevent execution with explicitly disabled context" <|
            res = Panic.catch Any (Runtime.with_disabled_context Input environment=Runtime.current_execution_environment (in_fn 1)) p-> p.payload.to_text
            res . should_equal "(Forbidden_Operation.Error 'Input')"
        group_builder.specify "should be configurable" <|
            r1 = Runtime.with_enabled_context Input environment=Runtime.current_execution_environment <|
                Runtime.with_enabled_context Output environment=Runtime.current_execution_environment <|
                  in_fn (out_fn 10)
            r1.should_equal 22

            r2 = Panic.catch Any (Runtime.with_disabled_context Input environment=Runtime.current_execution_environment  <| in_fn (out_fn 10)) p-> p.payload.to_text
            r2 . should_equal "(Forbidden_Operation.Error 'Input')"

main =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter

